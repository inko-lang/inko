# Iterating over collections
#
# Iterators are objects that can be used to traverse collections such as an
# `Array` or a `Map`. Typically iterators are implemented in one of two
# ways:
#
# 1. Internal iterators: these kind of iterators take care of the iteration
#    process and operate using some kind of closure.
# 2. External iterators: these iterators use some kind of cursor stored
#    somewhere and require you to manually advance the iterator.
#
# Both have their benefits and drawbacks. Internal iterators are easy to
# implement and typically faster, as they don't require the allocation of
# additional data structures.
#
# External iterators can be composed together, suspended, and later resumed.
# External iterators can also be turned into internal iterators, while the
# inverse is not possible unless a language supports some form of coroutines or
# generators.
#
# Inko uses external iteration. To make it easier to write such iterators, it
# provides the `Enum` type, which can create iterators from a closure.
import std::cmp::Equal
import std::option::Option
import std::string::(ToString, StringBuffer)

# The "byte" that signals the end in a `Bytes` iterator.
let pub EOF = -1

# A generic iterator over a sequence of values of type `T`.
trait pub Iter[T] {
  # Returns the next value in the iterator.
  #
  # If a value is produced, it must be wrapped in a Some; otherwise a None is to
  # be returned.
  fn pub mut next -> Option[T]

  # Calls the block for every value in `self`.
  #
  # # Examples
  #
  #     let vals = [10, 20, 30]
  #     let iter = vals.iter
  #
  #     iter.each fn (num) {
  #       num # => 10, 20, 30
  #     }
  fn pub move each(block: fn (T)) {
    loop {
      match self.next {
        case Some(v) -> block.call(v)
        case _ -> return
      }
    }
  }

  # Calls the block for every value in `self`, passing an index and the value to
  # the block.
  #
  # # Examples
  #
  #     let vals = [10, 20, 30]
  #     let iter = vals.iter
  #
  #     iter.each_with_index fn (index, num) {
  #       index # => 0, 1, 2
  #       num   # => 10, 20, 30
  #     }
  fn pub move each_with_index(block: fn (Int, T)) {
    with_index.each fn move (pair) {
      match pair {
        case (index, value) -> block.call(index, value)
      }
    }
  }

  # Returns an `Iter` that yields a tuple of the index and the iterator value.
  #
  # # Examples
  #
  #     let iter = [10, 20].into_iter.with_index
  #
  #     iter.next # => Option.Some((0, 10))
  #     iter.next # => Option.Some((1, 20))
  fn pub move with_index -> Iter[(Int, T)] {
    let mut index = 0

    map fn move (val) { (index := index + 1, val) }
  }

  # Returns an `Iter` that transforms values of type `T` to values of type
  # `R`.
  #
  # # Examples
  #
  # Multiplying the numbers in an `Array`:
  #
  #     let values = [1, 2, 3]
  #
  #     values.iter.map fn (n) { n * 2 }.to_array # => [2, 4, 6]
  fn pub move map[R](block: fn (T) -> R) -> Iter[R] {
    Enum.new fn move {
      self.next.map fn (v) { block.call(v) }
    }
  }

  # Returns the first value for which the supplied `Block` returns `True`.
  #
  # This method will advance the `Iter` until either a value is found or we
  # run out of values.
  #
  # # Examples
  #
  # Finding the first number greated than 50 in an `Array`:
  #
  #     let numbers = [10, 20, 50, 80]
  #
  #     numbers.iter.find fn (number) { number > 50 } # => 80
  fn pub mut find(block: fn (ref T) -> Bool) -> Option[T] {
    loop {
      match self.next {
        case Some(v) -> if block.call(v) { return Option.Some(v) }
        case _ -> return Option.None
      }
    }
  }

  # Returns an `Iter` that combines `find` with `map`.
  #
  # For each value in `self`, the supplied closure is called. If the closure
  # returns a `Some`, the value is returned an iteration stops.
  #
  # # Examples
  #
  #     let vals = [10, 20, 30]
  #     let val = vals.into_iter.find_map fn (v) {
  #       if v == 20 { Option.Some(v.to_string) } else { Option.None }
  #     }
  #
  #     val # => Option.Some('20')
  fn pub mut find_map[R](block: fn (T) -> Option[R]) -> Option[R] {
    loop {
      match self.next {
        case Some(v) -> match block.call(v) {
          case Some(r) -> return Option.Some(r)
          case _ -> {}
        }
        case _ -> return Option.None
      }
    }
  }

  # Returns `True` if `self` contains any value for which the `block` argument
  # returned `True`.
  #
  # This method stops iterating over the values after the first matching value.
  #
  # # Examples
  #
  # Checking if an `Iter` contains a value:
  #
  #     [10, 20, 30].iter.any? fn (value) { value >= 20 } # => True
  fn pub mut any?(block: fn (T) -> Bool) -> Bool {
    loop {
      match self.next {
        case Some(v) -> if block.call(v) { return true }
        case _ -> return false
      }
    }
  }

  # Returns an `Iter` that only produces values for which the supplied block
  # returned `True`.
  #
  # # Examples
  #
  # Selecting only certain values in an `Iter`:
  #
  #     [10, 20, 30]
  #       .iter
  #       .select fn (value) { value > 10 }
  #       .to_array # => [20, 30]
  fn pub move select(block: fn (ref T) -> Bool) -> Iter[T] {
    Enum.new fn move {
      loop {
        match self.next {
          case Some(v) -> if block.call(v) { return Option.Some(v) }
          case _ -> return Option.None
        }
      }
    }
  }

  # Returns an `Iter` that both filters and maps at the same time.
  #
  # The new iterator yields the values for which the provided block returned a
  # `Some` value.
  #
  # # Examples
  #
  #     let vals = [Option.Some(10), Option.None, Option.som(30)]
  #     let iter = vals.into_iter.select_map fn (opt) { opt }
  #
  #     iter.next # => Option.Some(10)
  #     iter.next # => Option.Some(30)
  #     iter.next # => Option.None
  fn pub move select_map[R](block: fn (T) -> Option[R]) -> Iter[R] {
    Enum.new fn move {
      loop {
        match self.next {
          case Some(v) -> match block.call(v) {
            case Some(r) -> return Option.Some(r)
            case _ -> next
          }
          case _ -> return Option.None
        }
      }
    }
  }

  # Partitions the `Iter` into a tuple of two `Array` objects.
  #
  # The first value of the tuple contains all values for which the supplied
  # block returned `True`. The second value contains all values for which the
  # block returned `False`.
  #
  # # Examples
  #
  # Partitioning an `Iter`:
  #
  #     let values = [10, 20, 30, 40, 50]
  #     let pair = values.iter.partition fn (value) { value >= 30 }
  #
  #     pair.0 # => [30, 40, 50]
  #     pair.1 # => [10, 20]
  fn pub move partition(block: fn (ref T) -> Bool) -> (Array[T], Array[T]) {
    reduce(([], [])) fn move (acc, val) {
      if block.call(ref val) { acc.0.push(val) } else { acc.1.push(val) }

      acc
    }
  }

  # Returns `True` if the supplied block returns `True` for _all_ values in
  # `self`.
  #
  # This method stops iterating over the values after the first value for which
  # the block returns `False`.
  #
  # # Examples
  #
  # Checking if all values in an `Iter` match a condition:
  #
  #     [10, 20].iter.all? fn (value) { value.positive? } # => True
  #     [-1, 20].iter.all? fn (value) { value.positive? } # => False
  fn pub mut all?(block: fn (T) -> Bool) -> Bool {
    loop {
      match self.next {
        case Some(v) -> if block.call(v).false? { return false }
        case _ -> return true
      }
    }
  }

  # Zips two `Iter` objects together, producing a new `Iter` that produces a
  # tuple containing the values of both `Iter` objects.
  #
  # The returned `Iter` stops iterating when one of the two zipped `Iter`
  # objects runs out of values.
  #
  # # Examples
  #
  # Zipping two `Iter` objects together:
  #
  #     let a = [10, 20, 30]
  #     let b = [40, 50, 60]
  #     let zip = a.iter.zip(b.iter)
  #
  #     zip.next # => (10, 40)
  #
  # Zipping two `Iter` objects with a different number of values:
  #
  #     let a = [10, 20, 30]
  #     let b = [40, 50]
  #     let zip = a.iter.zip(b.iter)
  #
  #     zip.next # => (10, 40)
  fn pub move zip[U](other: Iter[U]) -> Iter[(T, U)] {
    Enum.new fn move { self.next.zip(other.next) }
  }

  # Combines all values in the iterator into the specified accumulator.
  #
  # This method takes two arguments:
  #
  # 1. An accumulator to combine all values into.
  # 2. A block that accepts two arguments: the accumulator and the current
  #    value.
  #
  # The block returns the accumulator to return for the next value. The return
  # value of this method is the final accumulator.
  #
  # This method is called `inject` or `fold` in other languages.
  #
  # # Examples
  #
  # Summing all integers in an iterator:
  #
  #     let total = [1, 2, 3].iter.reduce(0) fn (total, current) {
  #       total + current
  #     }
  #
  #     total # => 6
  #
  # At each step of the iteration, the `total` and `current` block arguments are
  # set to the following values:
  #
  # | Element | total | current | return value
  # |:--------|:------|:--------|:-----------
  # | 0       | 0     | 1       | 1
  # | 1       | 1     | 2       | 3
  # | 2       | 3     | 3       | 6
  #
  # For the last element the return value is `6`, so the return value of the
  # reduce method is also `6`.
  fn pub move reduce[A](accumulator: A, block: fn (A, T) -> A) -> A {
    let mut result = accumulator

    loop {
      match self.next {
        case Some(v) -> result = block.call(result, v)
        case _ -> return result
      }
    }
  }

  # Returns an `Iter` that produces chunks of values.
  #
  # Each chunk is up to the amount specified by the `size` argument. If the
  # number of values can't be evenly divided, the last chunk may contain fewer
  # than `size` elements.
  fn pub move chunks(size: Int) -> Iter[Array[T]] {
    Enum.new fn move {
      let chunk = []

      while chunk.length < size {
        match self.next {
          case Some(val) -> chunk.push(val)
          case _ -> break
        }
      }

      if chunk.empty? { Option.None } else { Option.Some(chunk) }
    }
  }

  # Collects all values in the `Iter` in an `Array`.
  #
  # This method will advance the iterator to the end.
  #
  # # Examples
  #
  # Transforming an `Iter` back into an `Array`:
  #
  #     [1, 2, 3].iter.to_array # => [1, 2, 3]
  fn pub move to_array -> Array[T] {
    reduce([]) fn (values, value) {
      values.push(value)
      values
    }
  }

  # Returns the number of values remaining in this iterator.
  #
  # This method advances the iterator to the end.
  #
  # # Examples
  #
  #     [1, 2, 3].iter.count # => 3
  fn pub move count -> Int {
    reduce(0) fn (count, _) { count + 1 }
  }
}

# A type that can be moved into an iterator.
trait pub IntoIter[T] {
  # Moves `self` into an iterator.
  fn pub move into_iter -> Iter[T]
}

# A type for easily creating iterators using blocks.
#
# `Enum` wraps a block that feeds the iterator the next value. This allows
# for stateful iterators, without having to define explicit iterator types and
# implementing the `Iter` trait for these types.
#
# # Examples
#
#     import std::iter::(Iter, Enum)
#
#     fn example(max: Int) -> Iter[Int] {
#       let mut index = 0
#
#       Enum.new fn move {
#         if index < max {
#           Option.Some(index := index + 1)
#         } else {
#           Option.None
#         }
#       }
#     }
#
#     fn main {
#       let nums = example(4)
#
#       nums.next # => Option.Some(0)
#       nums.next # => Option.Some(1)
#     }
class pub Enum[T] {
  let @block: fn -> Option[T]

  # Returns a new iterator using the block.
  fn pub static new(block: fn -> Option[T]) -> Enum[T] {
    Enum { @block = block }
  }

  # Returns an iterator that produces the given value once.
  #
  # # Examples
  #
  #     import std::iter::Enum
  #
  #     let vals = Enum.once(42)
  #
  #     vals.next # => Option.Some(42)
  #     vals.next # => Option.None
  fn pub static once(value: T) -> Enum[T] {
    let mut val = Option.Some(value)

    Enum { @block = fn move { val := Option.None } }
  }

  # Returns an iterator that iterates from zero up to (but not including) the
  # given limit.
  #
  # For each index in the range, the supplied block is called and passed the
  # current index. The return value of the closure is wrapped in a Some. The
  # iterator terminates when reaching the limit.
  #
  # Using this method once can easily create iterators over sequential values
  # such as `Array`, without having to manually increment indexes.
  #
  # # Examples
  #
  #     import std::iter::Enum
  #
  #     let vals = [10, 20]
  #     let iter = Enum.indexed(vals.length) fn (index) { vals[index] }
  #
  #     iter.next #=> Option.Some(10)
  #     iter.next #=> Option.Some(20)
  #     iter.next #=> Option.None
  fn pub static indexed(limit: Int, block: fn (Int) -> T) -> Enum[T] {
    let mut index = 0
    let wrapper = fn move {
      if index < limit {
        Option.Some(block.call(index := index + 1))
      } else {
        Option.None
      }
    }

    Enum { @block = wrapper }
  }
}

impl Iter[T] for Enum {
  fn pub mut next -> Option[T] {
    @block.call
  }
}

# An iterator over a sequence of bytes.
#
# A `Bytes` is a regular iterator, but introduces the extra method `next_byte`.
# This method is similar to `Iter.next` in that it advances the iterator, but
# instead of returning an `Option[Int]` it returns an `Int`. This allows `Bytes`
# to be used as both a regular iterator and a more specialised (and more
# efficient) iterator over (large) sequences of bytes.
#
# When implementing `Bytes` for a type, you must also implement `Iter`. The
# easiest way of doing this is to have `Iter.next` reuse the implementation of
# `Bytes.next_byte` like so:
#
#     impl Iter[Int] for MyType {
#       fn pub mut next -> Option[Int] {
#         match next_byte {
#           case EOF -> Option.None
#           case byte -> Option.Some(byte)
#         }
#       }
#     }
#
#     impl Bytes for MyType {
#       fn pub mut next_byte -> Int {
#         # ...
#       }
#     }
trait pub Bytes: Iter[Int] {
  # Returns the next byte in the iterator.
  #
  # If all input is consumed, this method must return `std::iter::EOF`.
  fn pub mut next_byte -> Int
}
