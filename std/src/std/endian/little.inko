# Methods for converting values to/from little-endian encoded bytes.

fn size_error(min: Int) -> Never {
  panic('the ByteArray must contain at least ${min} bytes')
}

# Writes a value interpreted as a 16-bits unsigned integer into `into` as a
# series of bytes, starting at the index `at`.
#
# If `value` is greater than the maximum value of a 16-bits unsigned integer,
# the additional bits are ignored (i.e. the value wraps around).
#
# # Panics
#
# This method panics if `into` doesn't contain at least 2 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 2)
#
# little.write_i16(65_000, into: bytes, at: 0)
# bytes # => ByteArray.from_array([232, 253])
# ```
fn pub write_i16(value: Int, into: mut ByteArray, at: Int) {
  if into.size - at < 2 { size_error(2) }

  (into.pointer as Int + at as Pointer[UInt16]).0 = value as UInt16
}

# Writes a value interpreted as a 32-bits unsigned integer into `into` as a
# series of bytes, starting at the index `at`.
#
# If `value` is greater than the maximum value of a 32-bits unsigned integer,
# the additional bits are ignored (i.e. the value wraps around).
#
# # Panics
#
# This method panics if `into` doesn't contain at least 4 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 4)
#
# little.write_i32(123456789, into: bytes, at: 0)
# bytes # => ByteArray.from_array([21, 205, 91, 7])
# ```
fn pub write_i32(value: Int, into: mut ByteArray, at: Int) {
  if into.size - at < 4 { size_error(4) }

  (into.pointer as Int + at as Pointer[UInt32]).0 = value as UInt32
}

# Writes a value interpreted as a 64-bits signed integer into `into` as a series
# of bytes, starting at the index `at`.
#
# # Panics
#
# This method panics if `into` doesn't contain at least 8 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 8)
#
# little.write_i64(123456789, into: bytes, at: 0)
# bytes # => ByteArray.from_array([21, 205, 91, 7])
# ```
fn pub write_i64(value: Int, into: mut ByteArray, at: Int) {
  if into.size - at < 8 { size_error(8) }

  (into.pointer as Int + at as Pointer[UInt64]).0 = value as UInt64
}

# Reads four bytes starting at `at` as a 16-bits signed integer.
#
# # Panics
#
# This method panics if `from` doesn't contain at least 2 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 2)
#
# little.write_i16(65_000, into: bytes, at: 0)
# little.read_i16(from: bytes, at: 0) # => 65_000
# ```
fn pub read_i16(from: ref ByteArray, at: Int) -> Int {
  if from.size - at < 2 { size_error(2) }

  (from.pointer as Int + at as Pointer[UInt16]).0 as Int
}

# Reads four bytes starting at `at` as a 32-bits signed integer.
#
# # Panics
#
# This method panics if `from` doesn't contain at least 4 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 4)
#
# little.write_i32(123456789, into: bytes, at: 0)
# little.read_i32(from: bytes, at: 0) # => 123456789
# ```
fn pub read_i32(from: ref ByteArray, at: Int) -> Int {
  if from.size - at < 4 { size_error(4) }

  (from.pointer as Int + at as Pointer[UInt32]).0 as Int
}

# Reads eight bytes starting at `at` as a 64-bits signed integer.
#
# # Panics
#
# This method panics if `from` doesn't contain at least 8 values starting at
# index `at`.
#
# # Examples
#
# ```inko
# import std.endian.little
#
# let bytes = ByteArray.filled(with: 0, times: 8)
#
# little.write_i64(123456789, into: bytes, at: 0)
# little.read_i64(from: bytes, at: 0) # => 123456789
# ```
fn pub read_i64(from: ref ByteArray, at: Int) -> Int {
  if from.size - at < 8 { size_error(8) }

  (from.pointer as Int + at as Pointer[UInt64]).0 as Int
}
