# Methods for converting values to/from big-endian encoded bytes.

# Writes a value interpreted as a 32-bits unsigned integer into `into` as a
# series of bytes, starting at the index `at`.
#
# If `value` is greater than the maximum value of a 32-bits unsigned integer,
# the additional bits are ignored (i.e. the value wraps around).
#
# # Examples
#
# ```inko
# import std.endian.big
#
# let bytes = ByteArray.filled(with: 0, times: 4)
#
# big.write_u32(123456789, into: bytes, at: 0)
# bytes # => ByteArray.from_array([7, 91, 205, 21])
# ```
fn pub write_u32(value: Int, into: mut ByteArray, at: Int) {
  into.set(at, value >> 24)
  into.set(at + 1, value >> 16)
  into.set(at + 2, value >> 8)
  into.set(at + 3, value)
}

# Writes a value interpreted as a 64-bits signed integer into `into` as a series
# of bytes, starting at the index `at`.
#
# # Examples
#
# ```inko
# import std.endian.big
#
# let bytes = ByteArray.filled(with: 0, times: 8)
#
# big.write_i64(123456789, into: bytes, at: 0)
# bytes # => ByteArray.from_array([7, 91, 205, 21])
# ```
fn pub write_i64(value: Int, into: mut ByteArray, at: Int) {
  into.set(at, value >> 56)
  into.set(at + 1, value >> 48)
  into.set(at + 2, value >> 40)
  into.set(at + 3, value >> 32)
  into.set(at + 4, value >> 24)
  into.set(at + 5, value >> 16)
  into.set(at + 6, value >> 8)
  into.set(at + 7, value)
}

# Reads four bytes starting at `at` as a 32-bits signed integer.
#
# # Panics
#
# This method panics if there are less than four bytes available starting at
# `at`.
#
# # Examples
#
# ```inko
# import std.endian.big
#
# let bytes = ByteArray.filled(with: 0, times: 4)
#
# big.write_u32(123456789, into: bytes, at: 0)
# big.read_u32(from: bytes, at: 0) # => 123456789
# ```
fn pub read_u32(from: ref ByteArray, at: Int) -> Int {
  (from.get(at) << 24)
    | (from.get(at + 1) << 16)
    | (from.get(at + 2) << 8)
    | from.get(at + 3)
}

# Reads eight bytes starting at `at` as a 64-bits signed integer.
#
# # Panics
#
# This method panics if there are less than eight bytes available starting at
# `at`.
#
# # Examples
#
# ```inko
# import std.endian.big
#
# let bytes = ByteArray.filled(with: 0, times: 8)
#
# big.write_i64(123456789, into: bytes, at: 0)
# big.read_i64(from: bytes, at: 0) # => 123456789
# ```
fn pub read_i64(from: ref ByteArray, at: Int) -> Int {
  (from.get(at) << 56)
    | (from.get(at + 1) << 48)
    | (from.get(at + 2) << 40)
    | (from.get(at + 3) << 32)
    | (from.get(at + 4) << 24)
    | (from.get(at + 5) << 16)
    | (from.get(at + 6) << 8)
    | from.get(at + 7)
}
