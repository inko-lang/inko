import std.fmt (fmt)
import std.io (Buffer, Error as IoError)
import std.json (
  Error, ErrorKind, Json, Number, ObjectParser, Parser, PullParser, Type,
)
import std.test (Tests)

fn parse_invalid(input: String) -> Option[String] {
  let bytes = Buffer.new(input.to_byte_array)

  Parser.new(bytes).parse.error.map(fn (v) { v.to_string })
}

fn parse(input: String) -> Result[Json, Error[IoError]] {
  Json.parse(Buffer.new(input))
}

fn pull(input: String) -> PullParser[Buffer[String], IoError] {
  PullParser.new(Buffer.new(input))
}

fn pub tests(t: mut Tests) {
  t.test('ErrorKind.==', fn (t) {
    t.equal(
      ErrorKind.RecursionLimitExceeded(1) as ErrorKind[IoError],
      ErrorKind.RecursionLimitExceeded(1),
    )
    t.equal(
      ErrorKind.InvalidSurrogate('a') as ErrorKind[IoError],
      ErrorKind.InvalidSurrogate('a'),
    )
    t.equal(ErrorKind.Read(IoError.Other(1)), ErrorKind.Read(IoError.Other(1)))

    t.not_equal(
      ErrorKind.RecursionLimitExceeded(1) as ErrorKind[IoError],
      ErrorKind.Generic('foo'),
    )
  })

  t.test('ErrorKind.fmt', fn (t) {
    t.equal(
      fmt(ErrorKind.RecursionLimitExceeded(1) as ErrorKind[IoError]),
      'RecursionLimitExceeded(1)',
    )
    t.equal(
      fmt(ErrorKind.InvalidSurrogate('a') as ErrorKind[IoError]),
      'InvalidSurrogate("a")',
    )
    t.equal(fmt(ErrorKind.Read(IoError.Other(1))), 'Read(Other(1))')
  })

  t.test('ErrorKind.to_string', fn (t) {
    t.equal(
      (ErrorKind.RecursionLimitExceeded(1) as ErrorKind[IoError]).to_string,
      'the maximum recursion depth of 1 is exceeded',
    )
    t.equal((ErrorKind.Generic('foo') as ErrorKind[IoError]).to_string, 'foo')
  })

  t.test('Error.fmt', fn (t) {
    let err = Error(ErrorKind.Generic('foo'), offset: 5) as Error[IoError]

    t.equal(fmt(err), 'Error(kind: Generic("foo"), offset: 5)')
  })

  t.test('Error.==', fn (t) {
    let err1 = Error(ErrorKind.Generic('a'), offset: 5) as Error[IoError]
    let err2 = Error(ErrorKind.Generic('a'), offset: 5)
    let err3 = Error(ErrorKind.RecursionLimitExceeded(1), offset: 6)

    t.equal(err1, err2)
    t.not_equal(err1, err3)
  })

  t.test('Error.to_string', fn (t) {
    let err = Error(ErrorKind.Generic('test'), offset: 5) as Error[IoError]

    t.equal(err.to_string, 'test, at byte offset 5')
  })

  t.test('Error.generic', fn (t) {
    let err = Error.generic('foo', offset: 42) as Error[IoError]

    t.equal(err, Error(kind: ErrorKind.Generic('foo'), offset: 42))
  })

  t.test('PullParser.value_type', fn (t) {
    t.equal(pull('10').value_type, Result.Ok(Type.Number))
    t.equal(pull('10.2').value_type, Result.Ok(Type.Number))
    t.equal(pull('"a"').value_type, Result.Ok(Type.String))
    t.equal(pull('true').value_type, Result.Ok(Type.Bool))
    t.equal(pull('false').value_type, Result.Ok(Type.Bool))
    t.equal(pull('null').value_type, Result.Ok(Type.Null))
    t.equal(pull('[]').value_type, Result.Ok(Type.Array))
    t.equal(pull('{}').value_type, Result.Ok(Type.Object))
  })

  t.test('PullParser.skip', fn (t) {
    let parser = pull('10')

    t.equal(parser.skip, Result.Ok(nil))
    t.equal(parser.offset, 1)
  })

  t.test('PullParser.skip with invalid data', fn (t) {
    t.true(pull('10.a').skip.error?)
    t.true(pull('[').skip.error?)
    t.true(pull('{').skip.error?)
    t.true(pull('"').skip.error?)
    t.true(pull('t').skip.error?)
    t.true(pull('f').skip.error?)
  })

  t.test('PullParser.values with valid input', fn (t) {
    let parser = pull('[10,20]')
    let values = []
    let res = parser.values(fn { parser.int.map(fn (v) { values.push(v) }) })

    t.true(res.ok?)
    t.equal(values, [10, 20])
  })

  t.test('PullParser.values with invalid input', fn (t) {
    for input in ['', '10', '[,]', '[10,]'] {
      let parser = pull(input)

      t.true(parser.values(fn { parser.int.map(fn (_) { nil }) }).error?)
    }
  })

  t.test('PullParser.keys with valid input', fn (t) {
    let parser = pull('{"a": 10, "b": 20}')
    let positions = []
    let values = []
    let res = parser.keys(fn (pos, k) {
      parser.int.map(fn (v) {
        positions.push(pos)
        values.push((k, v))
      })
    })

    t.true(res.ok?)
    t.equal(values, [('a', 10), ('b', 20)])
    t.equal(positions, [1, 10])
  })

  t.test('PullParser.keys with invalid input', fn (t) {
    for input in ['', '10', '{,}', '{"a":10,}'] {
      let parser = pull(input)

      t.true(parser.keys(fn (_p, k) { parser.int.map(fn (_) { nil }) }).error?)
    }
  })

  t.test('PullParser.bool', fn (t) {
    t.equal(pull('true').bool, Result.Ok(true))
    t.equal(pull('false').bool, Result.Ok(false))
    t.true(pull('null').bool.error?)
    t.true(pull('').bool.error?)
  })

  t.test('PullParser.null', fn (t) {
    t.equal(pull('null').null, Result.Ok(nil))
    t.true(pull('n').null.error?)
    t.true(pull('').null.error?)
  })

  t.test('PullParser.string', fn (t) {
    t.equal(pull('"hello"').string, Result.Ok('hello'))
    t.true(pull('"hello').string.error?)
    t.true(pull('').string.error?)
  })

  t.test('PullParser.int', fn (t) {
    t.equal(pull('10').int, Result.Ok(10))
    t.equal(pull('10.5').int, Result.Ok(10))
    t.true(pull('null').int.error?)
    t.true(pull('').int.error?)
  })

  t.test('PullParser.float', fn (t) {
    t.equal(pull('10.5').float, Result.Ok(10.5))
    t.equal(pull('10').float, Result.Ok(10.0))
    t.true(pull('null').float.error?)
    t.true(pull('').float.error?)
  })

  t.test('PullParser.number', fn (t) {
    t.equal(pull('10.5').number, Result.Ok(Number.Float(10.5)))
    t.equal(pull('10').number, Result.Ok(Number.Int(10)))
    t.true(pull('null').number.error?)
    t.true(pull('').number.error?)
  })

  t.test('PullParser.offset', fn (t) {
    let parser = pull('10')

    parser.offset = 10
    t.equal(parser.offset, 10)
  })

  t.test('PullParser.start_of_next_value', fn (t) {
    t.equal(pull('10').start_of_next_value, Result.Ok(0))
    t.equal(pull('  10').start_of_next_value, Result.Ok(2))
    t.equal(pull('  ,').start_of_next_value, Result.Ok(2))
  })

  t.test('PullParser.object', fn (t) {
    let parser = pull('{ "a": 10 }')
    let nums = []
    let res = parser.object(fn (o) { o.int('a', fn (v) { nums.push(v) }) })

    t.true(res.ok?)
    t.equal(nums, [10])
  })

  t.test('ObjectParser.key', fn (t) {
    let parser = pull('{"a":10,"b":10}')
    let values = []
    let obj = ObjectParser.new

    obj.key('a', fn (p) { p.int.map(fn (v) { values.push(v) }) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10])
  })

  t.test('ObjectParser.string', fn (t) {
    let parser = pull('{"a":"foo","b":10}')
    let values = []
    let obj = ObjectParser.new

    obj.string('a', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, ['foo'])
  })

  t.test('ObjectParser.int', fn (t) {
    let parser = pull('{"a":10,"b":10}')
    let values = []
    let obj = ObjectParser.new

    obj.int('a', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10])
  })

  t.test('ObjectParser.float', fn (t) {
    let parser = pull('{"a":10.5,"b":10}')
    let values = []
    let obj = ObjectParser.new

    obj.float('a', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10.5])
  })

  t.test('ObjectParser.bool', fn (t) {
    let parser = pull('{"a":true,"b":10}')
    let values = []
    let obj = ObjectParser.new

    obj.bool('a', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [true])
  })

  t.test('ObjectParser.keys', fn (t) {
    let parser = pull('{"a":{"foo":10}}')
    let values = []
    let positions = []
    let obj = ObjectParser.new

    obj.keys('a', fn (parser, pos, k) {
      positions.push(pos)
      parser.int.map(fn (v) { values.push((k, v)) })
    })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [('foo', 10)])
    t.equal(positions, [6])
  })

  t.test('ObjectParser.values', fn (t) {
    let parser = pull('{"a":[10,20]}')
    let values = []
    let obj = ObjectParser.new

    obj.values('a', fn (p) { p.int.map(fn (v) { values.push(v) }) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10, 20])
  })

  t.test('ObjectParser.parse with all required keys present', fn (t) {
    let parser = pull('{"a":10}')
    let values = []
    let obj = ObjectParser.new

    obj.int('a', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10])
  })

  t.test('ObjectParser.parse with a missing optional key', fn (t) {
    let parser = pull(' {"a":10} ')
    let values = []
    let obj = ObjectParser.new

    obj.int('a', fn (v) { values.push(v) })
    obj.int('b', fn (v) { values.push(v) }).optional

    let res = obj.parse(parser)

    t.true(res.ok?)
    t.equal(values, [10])
  })

  t.test('ObjectParser.parse with a missing key', fn (t) {
    let parser = pull(' {"a":10} ')
    let values = []
    let obj = ObjectParser.new

    obj.int('a', fn (v) { values.push(v) })
    obj.int('b', fn (v) { values.push(v) })

    let res = obj.parse(parser)

    t.equal(
      res,
      Result.Error(
        Error(kind: ErrorKind.Generic('the key "b" is required'), offset: 1),
      ),
    )
    t.equal(values, [10])
  })

  t.test('Json.fmt', fn (t) {
    let map = Map.new

    map.set('a', Json.Int(10))

    t.equal(fmt(Json.Int(42)), 'Int(42)')
    t.equal(fmt(Json.Float(42.0)), 'Float(42.0)')
    t.equal(fmt(Json.String('test')), 'String("test")')
    t.equal(fmt(Json.Array([Json.Int(10)])), 'Array([Int(10)])')
    t.equal(fmt(Json.Object(map)), 'Object({"a": Int(10)})')
    t.equal(fmt(Json.Bool(true)), 'Bool(true)')
    t.equal(fmt(Json.Bool(false)), 'Bool(false)')
    t.equal(fmt(Json.Null), 'Null')
  })

  t.test('Json.==', fn (t) {
    let map1 = Map.new
    let map2 = Map.new
    let map3 = Map.new

    map1.set('a', Json.Int(10))
    map2.set('a', Json.Int(10))
    map3.set('a', Json.Int(10))

    t.equal(Json.Int(10), Json.Int(10))
    t.not_equal(Json.Int(10), Json.Int(20))
    t.not_equal(Json.Int(10), Json.Float(20.0))

    t.equal(Json.Float(10.0), Json.Float(10.0))
    t.not_equal(Json.Float(10.0), Json.Float(20.0))
    t.not_equal(Json.Float(10.0), Json.Int(10))

    t.equal(Json.String('foo'), Json.String('foo'))
    t.not_equal(Json.String('foo'), Json.String('bar'))
    t.not_equal(Json.String('foo'), Json.Int(10))

    t.equal(Json.Array([Json.Int(10)]), Json.Array([Json.Int(10)]))
    t.not_equal(Json.Array([Json.Int(10)]), Json.Array([Json.Int(20)]))
    t.not_equal(Json.Array([Json.Int(10)]), Json.Int(10))

    t.equal(Json.Object(map1), Json.Object(map2))
    t.not_equal(Json.Object(map3), Json.Object(Map.new))
    t.not_equal(Json.Object(Map.new), Json.Int(10))

    t.equal(Json.Bool(true), Json.Bool(true))
    t.not_equal(Json.Bool(true), Json.Bool(false))
    t.not_equal(Json.Bool(true), Json.Int(10))

    t.equal(Json.Null, Json.Null)
    t.not_equal(Json.Null, Json.Int(10))
  })

  t.test('Json.to_string', fn (t) {
    let map = Map.new

    map.set('a', Json.Int(1))
    map.set('b', Json.Int(2))

    t.equal(Json.Int(42).to_string, '42')
    t.equal(Json.Float(1.2).to_string, '1.2')
    t.equal(Json.String('foo').to_string, '"foo"')
    t.equal(Json.String('a\nb').to_string, '"a\\nb"')
    t.equal(Json.String('a\rb').to_string, '"a\\rb"')
    t.equal(Json.String('a\tb').to_string, '"a\\tb"')
    t.equal(Json.String('a\u{C}b').to_string, '"a\\fb"')
    t.equal(Json.String('a\u{8}b').to_string, '"a\\bb"')
    t.equal(Json.String('a\\b').to_string, '"a\\\\b"')
    t.equal(Json.Array([]).to_string, '[]')
    t.equal(Json.Array([Json.Int(1), Json.Int(2)]).to_string, '[1, 2]')
    t.equal(Json.Object(map).to_string, '{"a": 1, "b": 2}')
    t.equal(Json.Object(Map.new).to_string, '{}')
    t.equal(Json.Bool(true).to_string, 'true')
    t.equal(Json.Bool(false).to_string, 'false')
    t.equal(Json.Null.to_string, 'null')
  })

  t.test('Json.to_pretty_string', fn (t) {
    t.equal(Json.Int(42).to_pretty_string, '42')
    t.equal(Json.Float(1.2).to_pretty_string, '1.2')
    t.equal(Json.String('foo').to_pretty_string, '"foo"')
    t.equal(Json.String('a\nb').to_pretty_string, '"a\\nb"')
    t.equal(Json.String('a\rb').to_pretty_string, '"a\\rb"')
    t.equal(Json.String('a\tb').to_pretty_string, '"a\\tb"')
    t.equal(Json.String('a\u{C}b').to_pretty_string, '"a\\fb"')
    t.equal(Json.String('a\u{8}b').to_pretty_string, '"a\\bb"')
    t.equal(Json.String('a\\b').to_pretty_string, '"a\\\\b"')
    t.equal(Json.Bool(true).to_pretty_string, 'true')
    t.equal(Json.Bool(false).to_pretty_string, 'false')
    t.equal(Json.Null.to_pretty_string, 'null')

    t.equal(Json.Array([]).to_pretty_string, '[]')
    t.equal(
      Json.Array([Json.Int(1), Json.Int(2)]).to_pretty_string,
      '[
  1,
  2
]',
    )

    t.equal(
      Json.Array([Json.Array([Json.Int(1), Json.Int(2)])]).to_pretty_string,
      '[
  [
    1,
    2
  ]
]',
    )

    let map1 = Map.new
    let map2 = Map.new
    let map3 = Map.new
    let map4 = Map.new

    map1.set('a', Json.Int(1))
    map1.set('b', Json.Int(2))
    map2.set('a', Json.Array([Json.Int(1), Json.Int(2)]))
    map3.set('a', Json.Int(1))
    map3.set('b', Json.Object(map2))
    map4.set('a', Json.Array([]))
    map4.set('b', Json.Object(Map.new))

    t.equal(Json.Object(Map.new).to_pretty_string, '{}')
    t.equal(
      Json.Object(map1).to_pretty_string,
      '{
  "a": 1,
  "b": 2
}',
    )

    t.equal(
      Json.Object(map3).to_pretty_string,
      '{
  "a": 1,
  "b": {
    "a": [
      1,
      2
    ]
  }
}',
    )

    t.equal(
      Json.Object(map4).to_pretty_string,
      '{
  "a": [],
  "b": {}
}',
    )
  })

  t.test('Json.parse with integers', fn (t) {
    t.equal(parse('0'), Result.Ok(Json.Int(0)))
    t.equal(parse('42'), Result.Ok(Json.Int(42)))
    t.equal(parse('  42'), Result.Ok(Json.Int(42)))
    t.equal(parse('42   '), Result.Ok(Json.Int(42)))
    t.equal(parse('\t42'), Result.Ok(Json.Int(42)))
    t.equal(parse('\r42'), Result.Ok(Json.Int(42)))
    t.equal(parse('-42'), Result.Ok(Json.Int(-42)))

    t.true(parse('00').error?)
    t.true(parse('10,').error?)
    t.true(parse('-').error?)
    t.true(parse('-01').error?)
    t.true(parse('01').error?)
    t.true(parse('1a').error?)
    t.true(parse('-a').error?)
    t.true(parse('- ').error?)
  })

  t.test('Json.parse with floats', fn (t) {
    t.equal(parse('  1.2'), Result.Ok(Json.Float(1.2)))
    t.equal(parse('1.2   '), Result.Ok(Json.Float(1.2)))
    t.equal(parse('1.2'), Result.Ok(Json.Float(1.2)))
    t.equal(parse('-1.2'), Result.Ok(Json.Float(-1.2)))
    t.equal(parse('1.2e+123'), Result.Ok(Json.Float(1.2e+123)))
    t.equal(parse('1.2e-123'), Result.Ok(Json.Float(1.2e-123)))
    t.equal(parse('1.2E+123'), Result.Ok(Json.Float(1.2e+123)))
    t.equal(parse('1.2E-123'), Result.Ok(Json.Float(1.2e-123)))
    t.equal(parse('-1.2E-123'), Result.Ok(Json.Float(-1.2e-123)))
    t.equal(parse('0.0'), Result.Ok(Json.Float(0.0)))
    t.equal(parse('0E0'), Result.Ok(Json.Float(0.0)))
    t.equal(parse('0e+1'), Result.Ok(Json.Float(0.0)))
    t.equal(parse('1.2E1'), Result.Ok(Json.Float(1.2e1)))
    t.equal(parse('1.2e1'), Result.Ok(Json.Float(1.2e1)))
    t.equal(
      parse('1.7976931348623157e+310'),
      Result.Ok(Json.Float(Float.infinity)),
    )
    t.equal(
      parse('4.940656458412465441765687928682213723651e-330'),
      Result.Ok(Json.Float(0.0)),
    )
    t.equal(
      parse(
        '-0.000000000000000000000000000000000000000000000000000000000000000000000000000001',
      ),
      Result.Ok(Json.Float(-1.0E-78)),
    )

    # These numbers are too big for regular integers, so we promote them to
    # floats.
    t.equal(
      parse('11111111111111111111111111111111111111111'),
      Result.Ok(Json.Float(11111111111111111111111111111111111111111.0)),
    )
    t.equal(
      parse('10000000000000000999'),
      Result.Ok(Json.Float(10000000000000000999.0)),
    )

    t.true(parse('00.0').error?)
    t.true(parse('1.2e').error?)
    t.true(parse('1.2e+').error?)
    t.true(parse('1.2e-').error?)
    t.true(parse('1.2E').error?)
    t.true(parse('1.2E+').error?)
    t.true(parse('1.2E-').error?)
    t.true(parse('1.2E+a').error?)
    t.true(parse('1.2E-a').error?)
    t.true(parse('0E').error?)
    t.true(parse('10.2,').error?)

    t.equal(
      parse_invalid('\n1.2e'),
      Option.Some(
        'all input is consumed but more input is expected, at byte offset 4',
      ),
    )
  })

  t.test('Json.parse with arrays', fn (t) {
    t.equal(parse('[]'), Result.Ok(Json.Array([])))
    t.equal(parse('[10]'), Result.Ok(Json.Array([Json.Int(10)])))
    t.equal(
      parse('[10, 20]'),
      Result.Ok(Json.Array([Json.Int(10), Json.Int(20)])),
    )

    t.true(parse('[').error?)
    t.true(parse(']').error?)
    t.true(parse('[,10]').error?)
    t.true(parse('[10,]').error?)
    t.true(parse('[10').error?)
    t.true(parse('[10,').error?)
    t.true(parse('[10true]').error?)
    t.true(parse('[],').error?)

    {
      let parser = Parser.new(Buffer.new('[[[[10]]]]'.to_byte_array))

      parser.max_depth = 2
      t.true(parser.parse.error?)
    }
  })

  t.test('Json.parse with booleans', fn (t) {
    t.equal(parse('true'), Result.Ok(Json.Bool(true)))
    t.equal(parse('false'), Result.Ok(Json.Bool(false)))

    t.true(parse('t').error?)
    t.true(parse('tr').error?)
    t.true(parse('tru').error?)
    t.true(parse('f').error?)
    t.true(parse('fa').error?)
    t.true(parse('fal').error?)
    t.true(parse('fals').error?)
  })

  t.test('Json.parse with NULL', fn (t) {
    t.equal(parse('null'), Result.Ok(Json.Null))

    t.true(parse('n').error?)
    t.true(parse('nu').error?)
    t.true(parse('nul').error?)
  })

  t.test('Json.parse with strings', fn (t) {
    t.equal(parse('"foo"'), Result.Ok(Json.String('foo')))
    t.equal(parse('"foo bar"'), Result.Ok(Json.String('foo bar')))
    t.equal(parse('"foo\\nbar"'), Result.Ok(Json.String('foo\nbar')))
    t.equal(parse('"foo\\tbar"'), Result.Ok(Json.String('foo\tbar')))
    t.equal(parse('"foo\\rbar"'), Result.Ok(Json.String('foo\rbar')))
    t.equal(parse('"foo\\bbar"'), Result.Ok(Json.String('foo\u{8}bar')))
    t.equal(parse('"foo\\fbar"'), Result.Ok(Json.String('foo\u{C}bar')))
    t.equal(parse('"foo\\"bar"'), Result.Ok(Json.String('foo"bar')))
    t.equal(parse('"foo\\/bar"'), Result.Ok(Json.String('foo/bar')))
    t.equal(parse('"foo\\\\bar"'), Result.Ok(Json.String('foo\\bar')))
    t.equal(parse('"foo\\u005Cbar"'), Result.Ok(Json.String('foo\\bar')))
    t.equal(parse('"foo\\u001Fbar"'), Result.Ok(Json.String('foo\u{1F}bar')))
    t.equal(parse('"\\uD834\\uDD1E"'), Result.Ok(Json.String('\u{1D11E}')))
    t.equal(
      parse('"\\uE000\\uE000"'),
      Result.Ok(Json.String('\u{E000}\u{E000}')),
    )

    t.true(parse('"\0"').error?)
    t.true(parse('"\n"').error?)
    t.true(parse('"\t"').error?)
    t.true(parse('"\r"').error?)
    t.true(parse('"\u{8}"').error?) # \b
    t.true(parse('"\u{C}"').error?) # \f

    t.true(parse('"\\x42"').error?)
    t.true(parse('"\\u1"').error?)
    t.true(parse('"\\u12"').error?)
    t.true(parse('"\\u123"').error?)
    t.true(parse('"\\u{XXXX}"').error?)
    t.true(parse('"\\uD834\\uE000"').error?)
    t.true(parse('"\\uD834\\uZZZZ"').error?)
    t.true(parse('"\\uDFFF\\uDFFF"').error?)

    t.equal(
      parse_invalid('"a'),
      Option.Some(
        'all input is consumed but more input is expected, at byte offset 1',
      ),
    )
  })

  t.test('Json.parse with objects', fn (t) {
    let map1 = Map.new
    let map2 = Map.new
    let map3 = Map.new
    let map4 = Map.new
    let map5 = Map.new

    map2.set('a', Json.Int(10))
    map3.set('a', Json.Int(20))
    map4.set('a', Json.Int(10))
    map4.set('b', Json.Int(20))
    map5.set('a', Json.Int(10))
    map5.set('b', Json.Int(20))

    t.equal(parse('{}'), Result.Ok(Json.Object(map1)))
    t.equal(parse('{ "a" : 10 }'), Result.Ok(Json.Object(map2)))
    t.equal(parse('{"a": 10, "a": 20}'), Result.Ok(Json.Object(map3)))
    t.equal(parse('{"a": 10, "b": 20}'), Result.Ok(Json.Object(map4)))
    t.equal(
      parse(
        '{
          "a": 10,
          "b": 20
        }',
      ),
      Result.Ok(Json.Object(map5)),
    )

    t.true(parse('{').error?)
    t.true(parse('}').error?)
    t.true(parse('{{}}').error?)
    t.true(parse('{"a"}').error?)
    t.true(parse('{"a":}').error?)
    t.true(parse('{"a":10,}').error?)
    t.true(parse('{"a":10 "b": 20}').error?)
    t.true(parse('{a":10,}').error?)
    t.true(parse('{},').error?)
    t.true(parse('{"a": true} "x"').error?)

    {
      let bytes = Buffer.new('{"a": {"b": {"c": 10}}}'.to_byte_array)
      let parser = Parser.new(bytes)

      parser.max_depth = 2
      t.true(parser.parse.error?)
    }

    t.equal(
      parse_invalid('{"a"}'),
      Option.Some(
        "the character ':' is expected but '}' was found instead, at byte offset 4",
      ),
    )
  })

  t.test('Json.parse with Unicode BOMs', fn (t) {
    t.true(parse('\u{FEFF}10').error?)
    t.true(parse('\u{FFFE}10').error?)
    t.true(parse('\u{EF}\u{BB}\u{BF}10').error?)
  })

  t.test('Json.query', fn (t) {
    t.equal(Json.Int(42).query.as_int, Option.Some(42))
    t.equal(Json.String('test').query.as_int, Option.None)
  })

  t.test('Query.key', fn (t) {
    let map = Map.new

    map.set('name', Json.String('Alice'))

    let obj = Json.Object(map)

    t.equal(obj.query.key('name').as_string, Option.Some('Alice'))
    t.equal(obj.query.key('city').as_string, Option.None)
    t.equal(Json.Int(42).query.key('name').as_string, Option.None)
  })

  t.test('Query.index', fn (t) {
    t.equal(Json.Array([Json.Int(42)]).query.index(0).as_int, Option.Some(42))
    t.equal(Json.Array([Json.Int(42)]).query.index(1).as_int, Option.None)
    t.equal(Json.Int(42).query.index(0).as_int, Option.None)
  })

  t.test('Query.as_int', fn (t) {
    t.equal(Json.String('test').query.as_int, Option.None)
    t.equal(Json.Float(1.2).query.as_int, Option.None)
    t.equal(Json.Null.query.as_int, Option.None)
    t.equal(Json.Array([]).query.as_int, Option.None)
    t.equal(Json.Object(Map.new).query.as_int, Option.None)
    t.equal(Json.Int(42).query.as_int, Option.Some(42))
  })

  t.test('Query.as_float', fn (t) {
    t.equal(Json.String('test').query.as_float, Option.None)
    t.equal(Json.Null.query.as_float, Option.None)
    t.equal(Json.Int(42).query.as_float, Option.None)
    t.equal(Json.Array([]).query.as_float, Option.None)
    t.equal(Json.Object(Map.new).query.as_float, Option.None)
    t.equal(Json.Float(1.2).query.as_float, Option.Some(1.2))
  })

  t.test('Query.as_string', fn (t) {
    t.equal(Json.Null.query.as_string, Option.None)
    t.equal(Json.Int(42).query.as_string, Option.None)
    t.equal(Json.Float(1.2).query.as_string, Option.None)
    t.equal(Json.Array([]).query.as_string, Option.None)
    t.equal(Json.Object(Map.new).query.as_string, Option.None)
    t.equal(Json.String('test').query.as_string, Option.Some('test'))
  })

  t.test('Query.as_array', fn (t) {
    t.equal(Json.Null.query.as_array, Option.None)
    t.equal(Json.Int(42).query.as_array, Option.None)
    t.equal(Json.Float(1.2).query.as_array, Option.None)
    t.equal(Json.Object(Map.new).query.as_array, Option.None)
    t.equal(Json.String('test').query.as_array, Option.None)
    t.equal(
      Json.Array([Json.Int(42)]).query.as_array,
      Option.Some(ref [Json.Int(42)]),
    )
  })

  t.test('Query.as_object', fn (t) {
    t.equal(Json.Null.query.as_object, Option.None)
    t.equal(Json.Int(42).query.as_object, Option.None)
    t.equal(Json.Float(1.2).query.as_object, Option.None)
    t.equal(Json.String('test').query.as_object, Option.None)
    t.equal(Json.Array([Json.Int(42)]).query.as_object, Option.None)
    t.equal(Json.Object(Map.new).query.as_object, Option.Some(ref Map.new))
  })

  t.test('Query.as_bool', fn (t) {
    t.equal(Json.Bool(true).query.as_bool, Option.Some(true))
    t.equal(Json.Bool(false).query.as_bool, Option.Some(false))
    t.equal(Json.Int(42).query.as_bool, Option.None)
  })
}
