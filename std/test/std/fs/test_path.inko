import helpers (hash, with_directory)
import std.env
import std.fmt (fmt)
import std.fs (DirectoryEntry, FileType)
import std.fs.file (ReadOnlyFile, WriteOnlyFile)
import std.fs.path (self, Path)
import std.io (Error)
import std.stdio (Stdin, Stdout)
import std.test (Tests)
import std.time (DateTime, Duration)

fn valid_date?(time: Result[DateTime, Error]) -> Bool {
  match time {
    case Ok(t) -> {
      1970.to(2100).contains?(t.date.year)
        and 1.to(12).contains?(t.date.month)
        and 1.to(31).contains?(t.date.day)
    }
    case _ -> false
  }
}

fn read(from: ref Path) -> String {
  let file = ReadOnlyFile.new(from.clone).get
  let bytes = ByteArray.new
  let _ = file.read_all(bytes).get

  bytes.into_string
}

fn write(string: String, to: ref Path) {
  let file = WriteOnlyFile.new(to.clone).get

  file.write(string).get
}

fn strip_prefix(path: String, prefix: String) -> Option[Path] {
  Path.new(path).strip_prefix(Path.new(prefix))
}

fn extension(path: String) -> Option[String] {
  Path.new(path).extension.map(fn (v) { v.to_string })
}

fn components(path: String) -> Array[String] {
  Path.new(path).components.map(fn (v) { v.to_string }).to_array
}

fn pub tests(t: mut Tests) {
  t.test('path.SEPARATOR', fn (t) { t.equal(path.SEPARATOR, '/') })

  t.test('Path.file?', fn (t) {
    let path = env.temporary_directory.join('inko-test-${t.id}')

    t.false(path.file?)
    write('test', to: path)
    t.true(path.file?)

    path.remove_file.get
  })

  t.test('Path.directory?', fn (t) {
    t.true(env.temporary_directory.directory?)
    t.false(Path.new('inko-test-doesnt-exist').directory?)
  })

  t.test('Path.exists?', fn (t) {
    t.true(env.temporary_directory.exists?)
    t.false(Path.new('inko-test-doesnt-exist').exists?)
  })

  t.test('Path.absolute?', fn (t) {
    t.true(Path.new('/foo').absolute?)
    t.false(Path.new('foo').absolute?)
  })

  t.test('Path.relative?', fn (t) {
    t.true(Path.new('foo').relative?)
    t.false(Path.new('/foo').relative?)
  })

  t.test('Path.join', fn (t) {
    t.equal(Path.new('foo').join('bar'), Path.new('foo/bar'))
    t.equal(Path.new('foo').join('/').join('bar'), Path.new('/bar'))
    t.equal(Path.new('foo').join(''), Path.new('foo'))
    t.equal(Path.new('').join('foo'), Path.new('foo'))
    t.equal(Path.new('').join('/foo'), Path.new('/foo'))
  })

  t.test('Path.directory', fn (t) {
    t.equal(Path.new('foo').directory, Path.new('.'))
    t.equal(Path.new('foo/').directory, Path.new('.'))
    t.equal(Path.new('foo//').directory, Path.new('.'))
    t.equal(Path.new('foo/bar').directory, Path.new('foo'))
    t.equal(Path.new('foo/a/.').directory, Path.new('foo'))
    t.equal(Path.new('foo/bar.txt').directory, Path.new('foo'))
    t.equal(Path.new('foo//bar.txt').directory, Path.new('foo'))
    t.equal(Path.new('foo/./bar.txt').directory, Path.new('foo'))
    t.equal(Path.new('./foo.txt').directory, Path.new('.'))
    t.equal(Path.new('/foo/./bar.txt').directory, Path.new('/foo'))
    t.equal(Path.new('/foo/./bar/baz.txt').directory, Path.new('/foo/bar'))
    t.equal(Path.new('a/b/..').directory, Path.new('a/b'))
    t.equal(Path.new('').directory, Path.new('.'))
    t.equal(Path.new('..').directory, Path.new('.'))
    t.equal(Path.new('/..').directory, Path.new('/'))
    t.equal(Path.new('/').directory, Path.new('/'))
    t.equal(Path.new('/.').directory, Path.new('/'))
    t.equal(Path.new('a/.').directory, Path.new('.'))
    t.equal(Path.new('//').directory, Path.new('/'))
    t.equal(Path.new('.//').directory, Path.new('.'))
  })

  t.test('Path.==', fn (t) {
    t.equal(Path.new('foo'), Path.new('foo'))
    t.not_equal(Path.new('foo'), Path.new('bar'))
  })

  t.test('Path.to_string', fn (t) { t.equal(Path.new('foo').to_string, 'foo') })

  t.test('Path.into_string', fn (t) {
    t.equal(Path.new('foo').into_string, 'foo')
  })

  t.test('Path.clone', fn (t) {
    t.equal(Path.new('foo').clone, Path.new('foo'))
  })

  t.test('Path.fmt', fn (t) { t.equal(fmt(Path.new('foo')), '"foo"') })

  t.ok('Path.expand', fn (t) {
    try with_directory(t.id, fn (temp) {
      let bar = temp.join('foo').join('bar')

      try bar.create_directory_all

      let expanded = bar.join('..').join('..').expand

      t.equal(expanded, Result.Ok(temp.clone))
      Result.Ok(nil)
    })

    t.equal(Path.new('~').expand.ok, env.home_directory)
    t.equal(Path.new('~/').expand.ok, env.home_directory)
    t.true(Path.new('~foo').expand.error?)
    t.true(Path.new('/~').expand.error?)
    t.true(Path.new('~/this-directory-should-not-exist').expand.error?)
    Result.Ok(nil)
  })

  t.fork(
    'Path.expand with a missing home directory',
    child: fn {
      let out = Stdout.new
      let res = Path.new('~').expand.map(fn (v) { v.to_string }).or('ERROR')

      out.write(res)
    },
    test: fn (test, proc) {
      proc.variable('HOME', '')
      test.equal(proc.spawn.stdout, 'ERROR')
    },
  )

  t.test('Path.tail', fn (t) {
    t.equal(Path.new('foo').tail.to_string, 'foo')
    t.equal(Path.new('foo/').tail.to_string, 'foo')
    t.equal(Path.new('foo//').tail.to_string, 'foo')
    t.equal(Path.new('foo/bar').tail.to_string, 'bar')
    t.equal(Path.new('foo/a/.').tail.to_string, 'a')
    t.equal(Path.new('foo/bar.txt').tail.to_string, 'bar.txt')
    t.equal(Path.new('foo//bar.txt').tail.to_string, 'bar.txt')
    t.equal(Path.new('foo/./bar.txt').tail.to_string, 'bar.txt')
    t.equal(Path.new('/foo/./bar.txt').tail.to_string, 'bar.txt')
    t.equal(Path.new('').tail.to_string, '')
    t.equal(Path.new('..').tail.to_string, '..')
    t.equal(Path.new('/..').tail.to_string, '..')
    t.equal(Path.new('/').tail.to_string, '')
    t.equal(Path.new('/.').tail.to_string, '')
    t.equal(Path.new('a/.').tail.to_string, 'a')
    t.equal(Path.new('//').tail.to_string, '')
    t.equal(Path.new('.//').tail.to_string, '')
  })

  t.ok('Path.list with a valid directory', fn (t) {
    with_directory(t.id, fn (root) {
      let foo = root.join('foo')

      try root.create_directory
      try foo.create_directory

      let entry = root.list.get.select_map(fn (e) { e.ok }).find(fn (e) {
        e.path == foo
      })

      t.equal(
        entry,
        Option.Some(DirectoryEntry(path: foo, type: FileType.Directory)),
      )
      Result.Ok(nil)
    })
  })

  t.test('Path.list with an invalid directory', fn (t) {
    with_directory(t.id, fn (root) { t.true(root.list.error?) })
  })

  t.ok('Path.list_recursive with a valid directory', fn (t) {
    with_directory(t.id, fn (root) {
      let foo = root.join('foo')
      let bar = foo.join('bar')
      let baz = bar.join('baz')

      try baz.create_directory_all
      write('a', foo.join('a.txt'))
      write('b', foo.join('b.txt'))
      write('c', bar.join('c.txt'))
      write('d', baz.join('d.txt'))

      let paths = root
        .list_all
        .map(fn (i) {
          i
            .select_map(fn (r) { r.ok })
            .map(fn (e) {
              match e {
                case { @path = p } -> p
              }
            })
            .to_array
        })
        .or_else(fn (_) { [] })

      t.equal(paths.size, 4)
      t.true(paths.contains?(foo.join('a.txt')))
      t.true(paths.contains?(foo.join('b.txt')))
      t.true(paths.contains?(bar.join('c.txt')))
      t.true(paths.contains?(baz.join('d.txt')))
      Result.Ok(nil)
    })
  })

  t.test('Path.list_recursive with an invalid directory', fn (t) {
    with_directory(t.id, fn (root) { t.true(root.list_all.error?) })
  })

  t.test('Path.remove_file', fn (t) {
    let path = env.temporary_directory.join('inko-test-${t.id}')

    t.true(path.remove_file.error?)

    write('test', to: path)

    t.true(path.remove_file.ok?)
    t.false(path.exists?)
  })

  t.ok('Path.remove_directory', fn (t) {
    with_directory(t.id, fn (path) {
      t.true(path.remove_directory.error?)

      try path.create_directory

      t.true(path.remove_directory.ok?)
      t.false(path.directory?)
      Result.Ok(nil)
    })
  })

  t.test('Path.create_directory', fn (t) {
    with_directory(t.id, fn (path) {
      t.true(path.create_directory.ok?)
      t.true(path.directory?)
      t.true(path.create_directory.error?)
    })
  })

  t.test('Path.create_directory_all', fn (t) {
    with_directory(t.id, fn (root) {
      let path = root.join('foo').join('bar')

      t.true(path.create_directory_all.ok?)
      t.true(path.directory?)
      t.true(path.create_directory_all.ok?)
    })
  })

  t.ok('Path.remove_directory', fn (t) {
    with_directory(t.id, fn (path) {
      try path.create_directory

      t.true(path.remove_directory.ok?)
      t.true(path.remove_directory.error?)
      t.false(path.directory?)
      Result.Ok(nil)
    })
  })

  t.ok('Path.remove_directory_all', fn (t) {
    with_directory(t.id, fn (root) {
      let path = root.join('foo').join('bar')

      try path.create_directory_all

      t.true(root.remove_directory_all.ok?)
      t.true(root.remove_directory_all.error?)
      t.false(root.directory?)
      Result.Ok(nil)
    })
  })

  t.ok('Path.copy using regular files', fn (t) {
    # Source and destination on the same file system.
    let path1 = env.temporary_directory.join('inko-test-${t.id}-1')
    let path2 = env.temporary_directory.join('inko-test-${t.id}-2')

    {
      write('test', to: path1)

      t.true(path1.copy(to: path2).ok?)
      t.equal(read(path2), 'test')

      let meta1 = try path1.metadata
      let meta2 = try path2.metadata

      t.equal(meta1.size, meta2.size)
      t.equal(meta1.mode, meta2.mode)
    }

    # Source and destination (almost certainly) not on the same file system.
    t.true('fixtures/hello.txt'.to_path.copy(to: path2).ok?)
    t.equal(read(path2), 'hello\n')

    path1.remove_file.get
    path2.remove_file.get
    Result.Ok(nil)
  })

  t.fork(
    'Path.copy with a special file as the source',
    child: fn {
      let buf = ByteArray.new
      let _ = Stdin.new.read_all(buf).get
      let path = buf.into_string.to_path
      let _ = Path.new('/dev/stdin').copy(path).get
      let _ = path.remove_file.get
      let _ = Stdout.new.write('ok')
    },
    test: fn (t, proc) {
      let path = env.temporary_directory.join('inko-test-${t.id}')

      proc.stdin(path.to_string)
      t.equal(proc.spawn.stdout, 'ok')
    },
  )

  t.test('Path.extension', fn (t) {
    t.equal(Path.new('').extension, Option.None)
    t.equal(Path.new(' ').extension, Option.None)
    t.equal(Path.new('/').extension, Option.None)
    t.equal(Path.new('//').extension, Option.None)
    t.equal(Path.new('/a/').extension, Option.None)
    t.equal(Path.new('.').extension, Option.None)
    t.equal(Path.new('.a').extension, Option.None)
    t.equal(Path.new('foo').extension, Option.None)
    t.equal(Path.new('.foo').extension, Option.None)
    t.equal(Path.new('..').extension, Option.None)
    t.equal(Path.new('...').extension, Option.None)
    t.equal(Path.new('..a.').extension, Option.None)
    t.equal(Path.new('..a..').extension, Option.None)
    t.equal(Path.new('foo.').extension, Option.None)

    t.equal(extension('.foo.txt'), Option.Some('txt'))
    t.equal(extension('.foo.html.md'), Option.Some('md'))
    t.equal(extension('foo.txt'), Option.Some('txt'))
    t.equal(extension('foo.a b'), Option.Some('a b'))
    t.equal(extension('foo.html.md'), Option.Some('md'))
    t.equal(extension('a/foo.txt'), Option.Some('txt'))
    t.equal(extension('a/foo.html.md'), Option.Some('md'))
    t.equal(extension('/foo.txt'), Option.Some('txt'))
    t.equal(extension('/foo.html.md'), Option.Some('md'))
    t.equal(extension('/a/b.txt'), Option.Some('txt'))
    t.equal(extension('//b.txt'), Option.Some('txt'))
    t.equal(extension('foo.aðŸ˜€a'), Option.Some('aðŸ˜€a'))
    t.equal(extension('...a'), Option.Some('a'))
    t.equal(extension('/./b.txt'), Option.Some('txt'))
  })

  t.test('Path.hash', fn (t) {
    t.equal(hash(Path.new('foo')), hash(Path.new('foo')))
  })

  t.test('Path.components', fn (t) {
    t.equal(components(''), [])
    t.equal(components('a'), ['a'])
    t.equal(components('a/b/c'), ['a', 'b', 'c'])
    t.equal(components('a//b//c'), ['a', 'b', 'c'])
    t.equal(components('a/./b/c'), ['a', 'b', 'c'])
    t.equal(components('a/.b/c'), ['a', '.b', 'c'])
    t.equal(components('a/../b/c'), ['a', '..', 'b', 'c'])
    t.equal(components('./a/b/c'), ['a', 'b', 'c'])
    t.equal(components('/'), ['/'])
    t.equal(components('/.'), ['/'])
    t.equal(components('/./'), ['/'])
    t.equal(components('/./a'), ['/', 'a'])
    t.equal(components('/./a/.'), ['/', 'a'])
    t.equal(components('//'), ['/'])
    t.equal(components('./'), [])
    t.equal(components('.//'), [])
    t.equal(components('.///'), [])
    t.equal(components('.//./'), [])
    t.equal(components('./././'), [])
    t.equal(components('/a/b/c'), ['/', 'a', 'b', 'c'])
    t.equal(components('/./a/b/c'), ['/', 'a', 'b', 'c'])
    t.equal(components('//a/b/c'), ['/', 'a', 'b', 'c'])
    t.equal(components('//a/./b/c'), ['/', 'a', 'b', 'c'])
    t.equal(components('a/ðŸ˜€/b'), ['a', 'ðŸ˜€', 'b'])
  })

  t.test('Path.strip_prefix', fn (t) {
    t.equal(strip_prefix('foo/bar', 'foo'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('foo/bar', 'foo/'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('foo/bar', 'foo/bar'), Option.Some(Path.new('')))
    t.equal(strip_prefix('foo/bar', 'foo/bar/'), Option.Some(Path.new('')))
    t.equal(strip_prefix('/foo/bar', '/'), Option.Some(Path.new('foo/bar')))
    t.equal(strip_prefix('/foo/bar', '/foo'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('/foo/bar', '/foo/'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('/foo/bar', '//foo//'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('/ðŸ˜€/foo', '/ðŸ˜€'), Option.Some(Path.new('foo')))
    t.equal(strip_prefix('./foo/bar', './'), Option.Some(Path.new('foo/bar')))
    t.equal(strip_prefix('./foo/bar', './foo'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('~/foo/bar', '~/foo'), Option.Some(Path.new('bar')))
    t.equal(strip_prefix('~/foo/bar', '~'), Option.Some(Path.new('foo/bar')))
    t.equal(strip_prefix('~/foo/bar', '~/'), Option.Some(Path.new('foo/bar')))
    t.equal(strip_prefix('foo/bar', ''), Option.Some(Path.new('foo/bar')))
    t.equal(strip_prefix('', ''), Option.Some(Path.new('')))
    t.equal(strip_prefix('foo/bar', 'wat'), Option.None)
    t.equal(strip_prefix('', 'foo'), Option.None)
  })

  t.test('Path.with_extension', fn (t) {
    t.equal(Path.new('a').with_extension('b'), Path.new('a.b'))
    t.equal(Path.new('a.a').with_extension('b'), Path.new('a.b'))
    t.equal(Path.new('a.a.b').with_extension('c'), Path.new('a.a.c'))
    t.equal(Path.new('a.a.b').with_extension(''), Path.new('a.a'))
    t.equal(Path.new('a.a').with_extension(''), Path.new('a'))
    t.equal(Path.new('a').with_extension(''), Path.new('a'))
    t.equal(Path.new('.a').with_extension('b'), Path.new('.a.b'))
    t.equal(Path.new('').with_extension(''), Path.new(''))
    t.equal(Path.new('').with_extension('a'), Path.new(''))
    t.equal(Path.new('/').with_extension('a'), Path.new('/'))
    t.equal(Path.new('./').with_extension('a'), Path.new('./'))
  })

  t.panic('Path.with_extension with an invalid extension', fn {
    Path.new('a.txt').with_extension('txt/foo')
  })

  t.ok('Path.metadata', fn (t) {
    let path = env.temporary_directory.join('inko-test-${t.id}')

    write('test', path)

    let today = DateTime.utc - Duration.from_secs(60)
    let meta = try path.metadata

    t.true(meta.size >= 4)
    t.true(meta.type.file?)
    t.true(meta.modified_at >= today)
    t.true(meta.accessed_at >= today)

    match meta.created_at {
      case Some(v) -> t.true(v >= today)
      case _ -> {}
    }

    path.remove_file.get

    Result.Ok(nil)
  })
}
