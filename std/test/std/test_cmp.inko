import helpers::(fmt)
import std::cmp::(Compare, Equal, Ordering, max, min)
import std::test::Tests

class enum Letter {
  case A
  case B
}

impl Compare[Letter] for Letter {
  fn pub cmp(other: ref Letter) -> Ordering {
    match self {
      case A -> match other {
        case A -> Ordering.Equal
        case B -> Ordering.Less
      }
      case B -> match other {
        case A -> Ordering.Greater
        case B -> Ordering.Equal
      }
    }
  }
}

impl Equal[Letter] for Letter {
  fn pub ==(other: ref Letter) -> Bool {
    match self {
      case A -> match other {
        case A -> true
        case B -> false
      }
      case B -> match other {
        case A -> false
        case B -> true
      }
    }
  }
}

fn pub tests(t: mut Tests) {
  t.test('Ordering.==') fn (t) {
    t.equal(Ordering.Less, Ordering.Less)
    t.equal(Ordering.Greater, Ordering.Greater)
    t.equal(Ordering.Equal, Ordering.Equal)
    t.not_equal(Ordering.Less, Ordering.Greater)
    t.not_equal(Ordering.Less, Ordering.Equal)
    t.not_equal(Ordering.Greater, Ordering.Less)
    t.not_equal(Ordering.Greater, Ordering.Equal)
    t.not_equal(Ordering.Equal, Ordering.Less)
    t.not_equal(Ordering.Equal, Ordering.Greater)
  }

  t.test('Ordering.fmt') fn (t) {
    t.equal(fmt(Ordering.Less), 'Less')
    t.equal(fmt(Ordering.Greater), 'Greater')
    t.equal(fmt(Ordering.Equal), 'Equal')
  }

  t.test('Compare.<') fn (t) {
    t.true(Letter.A < Letter.B)
    t.false(Letter.B < Letter.A)
  }

  t.test('Compare.<=') fn (t) {
    t.true(Letter.A <= Letter.A)
    t.true(Letter.A <= Letter.B)
    t.false(Letter.B <= Letter.A)
  }

  t.test('Compare.>') fn (t) {
    t.true(Letter.B > Letter.A)
    t.false(Letter.A > Letter.B)
  }

  t.test('Compare.>=') fn (t) {
    t.true(Letter.B >= Letter.A)
    t.true(Letter.B >= Letter.B)
    t.false(Letter.A >= Letter.B)
  }

  t.test('Equal.==') fn (t) {
    t.true(Letter.A == Letter.A)
    t.true(Letter.A != Letter.B)
  }

  t.test('cmp.min') fn (t) {
    t.equal(min(10, 10), 10)
    t.equal(min(10, 5), 5)
    t.equal(min(5, 10), 5)
  }

  t.test('cmp.max') fn (t) {
    t.equal(max(10, 10), 10)
    t.equal(max(10, 5), 10)
    t.equal(max(5, 10), 10)
  }
}
