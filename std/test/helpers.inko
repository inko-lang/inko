import std::drop::(drop)
import std::env
import std::fmt::(DefaultFormatter, Format, Formatter)
import std::fs::file::(WriteOnlyFile, remove)
import std::fs::path::Path
import std::hash::(Hash, Hasher)
import std::hash::siphash::SipHasher13
import std::sys::(Command, Stream)

fn pub hash(value: ref Hash) -> Int {
  let hasher: Hasher = SipHasher13.default

  value.hash(hasher)
  hasher.finish
}

fn pub fmt(value: ref Format) -> String {
  let fmt: Formatter = DefaultFormatter.new

  value.fmt(fmt)
  fmt.into_string
}

fn pub compiler_path -> Path {
  let base = match env.working_directory {
    case Ok(path) -> path
    case Error(err) -> panic("Failed to determine the working directory: {err}")
  }

  let target = if base.join('test').directory? {
    base.join('..').join('target')
  } else if base.join('std').directory? {
    base.join('target')
  } else {
    panic('Tests must be run in either the project root, or the std/ directory')
  }

  let debug = target.join('debug').join('inko')
  let release = target.join('release').join('inko')

  match (debug.created_at, release.created_at) {
    case (Ok(deb), Ok(rel)) -> if deb >= rel { debug } else { release }
    case (Ok(_), Error(_)) -> debug
    case (Error(_), Ok(_)) -> release
    case _ -> panic("The path to the compiler couldn't be determined")
  }
}

class pub Script {
  let @id: Int
  let @path: Path
  let @code: String
  let @cmd: Command
  let @stdin: Option[String]
  let @imports: Array[String]

  fn pub static new(id: Int, code: String) -> Script {
    let path = env.temporary_directory.join("inko_test_script_{id}.inko")
    let exe = compiler_path
    let cmd = Command.new(exe)

    cmd.argument('run')
    cmd.argument('-f')
    cmd.argument('plain')
    cmd.argument(path.to_string)
    cmd.stdin(Stream.Null)
    cmd.stderr(Stream.Piped)
    cmd.stdout(Stream.Piped)

    Script {
      @id = id,
      @path = path,
      @code = code,
      @cmd = cmd,
      @stdin = Option.None,
      @imports = ['std::stdio::(STDIN, STDERR, STDOUT)']
    }
  }

  fn pub move import(module: String) -> Script {
    @imports.push(module)
    self
  }

  fn pub move stdin(input: String) -> Script {
    @stdin = Option.Some(input)

    @cmd.stdin(Stream.Piped)
    self
  }

  fn pub move argument(value: String) -> Script {
    @cmd.argument(value)
    self
  }

  fn pub move variable(name: String, value: String) -> Script {
    @cmd.variable(name, value)
    self
  }

  fn pub move run -> String {
    let file = WriteOnlyFile.new(@path.clone).unwrap

    @imports.into_iter.each fn (mod) {
      file.write_string("import {mod}\n").unwrap
    }

    file
      .write_string(
"class async Main \{
  fn async main \{
    {@code}
  }
}
"
      )
      .unwrap

    file.flush.unwrap

    let child = @cmd.spawn.unwrap
    let bytes = ByteArray.new

    match @stdin {
      case Some(input) -> child.stdin.write_string(input).unwrap
      case _ -> 0
    }

    child.wait.unwrap
    child.stdout.read_all(bytes).unwrap
    child.stderr.read_all(bytes).unwrap
    drop(file)
    remove(@path).unwrap
    bytes.into_string
  }
}
