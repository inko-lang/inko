# Types for binary and ternary tuples.
#
# This module only offers types for binary and ternary tuples. If you need a
# combination of more values (e.g. a 4-arity tuple), it's best to define your
# own type instead.
import std::hash::*
import std::operators::Equal

# A pair of two values.
class Pair!(A, B) {
  # The first value of this `Pair`.
  @first: A

  # The second value of this `Pair`.
  @second: B

  static def new(first: A, second: B) -> Self {
    Self { @first = first, @second = second }
  }

  # Returns the first value of this `Pair`.
  #
  # # Examples
  #
  # Obtaining the first value of a `Pair`:
  #
  #     import std::pair::Pair
  #
  #     let pair = Pair.new(first: 10, second: 'foo')
  #
  #     pair.first # => 10
  def first -> A {
    @first
  }

  # Returns the second value of this `Pair`.
  #
  # # Examples
  #
  # Obtaining the second value of a `Pair`:
  #
  #     import std::pair::Pair
  #
  #     let pair = Pair.new(first: 10, second: 'foo')
  #
  #     pair.second # => 'foo'
  def second -> B {
    @second
  }
}

impl Equal for Pair {
  def ==(other: Self) -> Boolean when A: Equal, B: Equal {
    (@first == other.first).and { @second == other.second }
  }
}

impl Hash for Pair {
  def hash(hasher: Hasher) when A: Hash, B: Hash {
    @first.hash(hasher)
    @second.hash(hasher)
  }
}

# A pair of three values.
class Triple!(A, B, C) {
  # The first value of this `Pair`.
  @first: A

  # The second value of this `Pair`.
  @second: B

  # The third value of this `Pair`.
  @third: C

  static def new(first: A, second: B, third: C) -> Self {
    Self { @first = first, @second = second, @third = third }
  }

  # Returns the first value of this `Triple`.
  #
  # # Examples
  #
  # Obtaining the first value of a `Triple`:
  #
  #     import std::pair::Triple
  #
  #     let triple = Triple.new(first: 10, second: 'foo', third: 20)
  #
  #     triple.first # => 10
  def first -> A {
    @first
  }

  # Returns the second value of this `Triple`.
  #
  # # Examples
  #
  # Obtaining the second value of a `Triple`:
  #
  #     import std::pair::Triple
  #
  #     let triple = Triple.new(first: 10, second: 'foo', third: 20)
  #
  #     triple.second # => 'foo'
  def second -> B {
    @second
  }

  # Returns the third value of this `Triple`.
  #
  # # Examples
  #
  # Obtaining the second value of a `Triple`:
  #
  #     import std::pair::Triple
  #
  #     let triple = Triple.new(first: 10, second: 'foo', third: 20)
  #
  #     triple.third # => 20
  def third -> C {
    @third
  }
}

impl Equal for Triple {
  def ==(other: Self) -> Boolean when A: Equal, B: Equal, C: Equal {
    (@first == other.first)
      .and { @second == other.second }
      .and { @third == other.third }
  }
}

impl Hash for Triple {
  def hash(hasher: Hasher) when A: Hash, B: Hash, C: Hash {
    @first.hash(hasher)
    @second.hash(hasher)
    @third.hash(hasher)
  }
}
