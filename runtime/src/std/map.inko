#! A hash map using linear probing and Robin Hood bucket stealing.
import std::hash::(Hash, Hasher)
import std::index::(Index, SetIndex)
import std::iterator::(Enumerator, Iterator)
import std::length::Length
import std::operators::Equal
import std::process
import std::random

## The load factor of a `Map` before it should be resized.
let LOAD_FACTOR = 0.75

## The default `Hasher` used for a `Map`.
##
## Different instances of a `DefaultHasher` may produce different hash values
## for the same object. The internal hashing algorithm may also change, and so
## the exact hash values should not be relied upon.
##
## Internally this hasher uses the hashing instructions provided by IVM, which
## currently uses SipHash 1-3.
object DefaultHasher {
  ## Returns a new `DefaultHasher`.
  ##
  ## The given keys will be used as secrets for the hasher.
  ##
  ## # Panics
  ##
  ## This method will panic if any of the provided keys are below zero.
  static def new(key0: Integer, key1: Integer) -> Self {
    _INKOC.hasher_new(DefaultHasher, key0, key1)
  }
}

impl Hasher for DefaultHasher {
  def write_integer(value: Integer) -> Integer {
    _INKOC.hasher_write(self, value)
  }

  def write_float(value: Float) -> Float {
    _INKOC.hasher_write(self, value)
  }

  def write_string(value: String) -> String {
    _INKOC.hasher_write(self, value)
  }

  def write_boolean(value: Boolean) -> Boolean {
    _INKOC.hasher_write(self, value)
  }

  def to_hash -> Integer {
    _INKOC.hasher_to_hash(self)
  }

  def reset -> Self {
    _INKOC.hasher_reset(self)
  }
}

## The state used for creating a `DefaultHasher`.
##
## Different `DefaultHasher` instances created from the same `RandomState` will
## produce the same hash values for the same input values. However, hashers
## created from different `RandomState` objects will produce different hashes.
object RandomState {
  ## The first randomly generated key to use for the hashing algorithm.
  @key0: Integer

  ## The second randomly generated key to use for the hashing algorithm.
  @key1: Integer

  def init {
    @key0 = random.incremental_integer
    @key1 = random.incremental_integer
  }

  ## Creates a new `DefaultHasher`.
  def to_hasher -> DefaultHasher {
    DefaultHasher.new(key0: @key0, key1: @key1)
  }
}

## A key-value pair with a pre-computed hash.
object Pair!(K: Hash + Equal, V) {
  ## The key that was hashed.
  @key: K

  ## The value of the key.
  @value: V

  ## The hash value of the key.
  @hash: Integer

  ## The distance of this pair relative to its desired index.
  @distance: Integer

  def init(key: K, value: V, hash: Integer) {
    @key = key
    @value = value
    @hash = hash
    @distance = 0
  }

  def distance -> Integer {
    @distance
  }

  def reset_distance -> Integer {
    @distance = 0
  }

  def increase_distance -> Integer {
    @distance += 1
  }

  def reduce_distance -> Integer {
    @distance -= 1
  }

  def replace_with?(other: Self) -> Boolean {
    distance < other.distance
  }

  def key -> K {
    @key
  }

  def value -> V {
    @value
  }

  def hash -> Integer {
    @hash
  }
}

## An unordered hash map using linear probing and Robin Hood bucket stealing.
##
## The keys in a `Map` can be any object that implements the `Hash` and
## `Equal` traits. The values can be of any type. It's possible to store keys
## (or values) of different types but this will require the use of `Dynamic`.
##
## A `Map` is unordered, meaning that keys can be returned in a (seemingly)
## random order.
##
## `Map` uses linear probing for finding values and Robin Hood hashing.
## Removals are performed using backwards shift deletion. For more information
## on these algorithms you can refer to the following resources:
##
## * http://codecapsule.com/2013/11/11/robin-hood-hashing/
## * http://codecapsule.com/2013/11/17/robin-hood-hashing-backward-shift-deletion/
## * https://www.sebastiansylvan.com/post/robin-hood-hashing-should-be-your-default-hash-table-implementation/
object Map!(K: Hash + Equal, V) {
  ## The state to use for creating hashers.
  @random_state: RandomState

  ## The buckets to store pairs in. Each bucket can only contain a single
  ## pair.
  @buckets: Array!(?Pair!(K, V))

  ## The number of key-value pairs stored in this `Map`.
  @length: Integer

  ## The number of values that can be stored before resizing.
  @capacity: Integer

  ## The number of values to store before a resize is necessary.
  ## We store this value explicitly so we don't have to allocate floats every
  ## time we want to check if we need to resize.
  @resize_threshold: Integer

  ## Returns a `Map` using two arrays: one containing the keys and one
  ## containing the values.
  ##
  ## # Examples
  ##
  ## Creating a `Map` from two arrays:
  ##
  ##     Map.from_array(Array.new('name'), Array.new('Alice'))
  static def from_array!(K: Hash + Equal, V)(
    keys: Array!(K),
    values: Array!(V)
  ) -> Map!(K, V) {
    let mut map = Map.new
    let max_index = values.length - 1

    keys.length == values.length
      .if_false {
        process.panic('An equal number of keys and values must be specified')
      }

    keys.each_with_index do (key: K, index: Integer) {
      index > max_index
        .if_true {
          return map
        }

      map[key] = values[index]!
    }

    map
  }

  def init {
    @random_state = RandomState.new
    @buckets = Array.new
    @length = 0
    @capacity = 1
    @resize_threshold = 1
  }

  ## Returns the buckets of this `Map`.
  def buckets -> Array!(?Pair!(K, V)) {
    @buckets
  }

  ## Removes the given key, returning its value if the key was present in the
  ## map.
  ##
  ## # Examples
  ##
  ## Removing a non-existing key:
  ##
  ##     let mut map = Map.new
  ##
  ##     map.remove('name') # => Nil
  ##
  ## Removing an existing key:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.remove('name') # => 'Alice'
  def remove(key: K) -> ?V {
    let index = _bucket_index(key)

    index.if true: {
      let pair = @buckets[index!]

      @buckets[index!] = Nil
      @length -= 1

      _backwards_shift(index! + 1)

      pair.value
    }, false: {
      Nil
    }
  }

  ## Calls the supplied `Block` for every key value pair in this map.
  ##
  ## This method differs from `Iterator.each` in that it yields the key and
  ## value as separate arguments instead of yielding a `Pair` object.
  ##
  ## # Examples
  ##
  ## Iterating over the keys and values of a `Map`:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.each do (key, value) {
  ##       key   # => 'name'
  ##       value # => 'Alice'
  ##     }
  def each(block: do (K, V)) {
    iter.each do (pair) {
      block.call(pair.key, pair.value)
    }
  }

  ## Returns an `Iterator` that iterates over all key-value pairs in this
  ## `Map`.
  ##
  ## # Examples
  ##
  ## Iterating over all the key-value pairs:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.iter.each do (pair) {
  ##       pair.key   # => 'name'
  ##       pair.value # => 'Alice'
  ##     }
  def iter -> Iterator!(Pair!(K, V)) {
    let mut index = 0
    let max = @buckets.length

    Enumerator.new(
      while: { index < max },
      yield: {
        let mut found: ?Pair!(K, V) = Nil

        {
          index < max
            .and { found == Nil }
        }.while_true {
          found = @buckets[index]
          index += 1
        }

        found
      }
    )
  }

  ## Returns an `Iterator` visiting all the keys in this `Map`.
  ##
  ## # Examples
  ##
  ## Iterating over the keys in a `Map`:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.keys.each do (key) {
  ##       key # => 'name'
  ##     }
  def keys -> Iterator!(K) {
    iter.map do (pair) { pair.key }
  }

  ## Returns an `Iterator` visiting all the values in this `Map`.
  ##
  ## # Examples
  ##
  ## Iterating over the values in a `Map`:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.values.each do (value) {
  ##       value # => 'Alice'
  ##     }
  def values -> Iterator!(V) {
    iter.map do (pair) { pair.value }
  }

  ## Returns `True` if `self` contains the key `key`.
  ##
  ## # Examples
  ##
  ## Checking if a `Map` defines a key:
  ##
  ##     let map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.key?('name') # => True
  ##     map.key?('city') # => False
  def key?(key: K) -> Boolean {
    _bucket_index(key).if true: {
      True
    }, false: {
      False
    }
  }

  ## Inserts the key and value in this `Map`, returning the `Map` itself.
  ##
  ## This method makes it possible to create a `Map` and store many key-value
  ## pairs, all in a single message chain.
  ##
  ## # Examples
  ##
  ## Inserting multiple key-value pairs:
  ##
  ##     let map = Map.new.set('a', 10).set('b', 20)
  def set(key: K, value: V) -> Self {
    @length >= @resize_threshold
      .if_true {
        rehash
      }

    _insert_pair(Pair.new(key: key, value: value, hash: _hash_key(key)))

    self
  }

  ## Resizes and rehashes `self`.
  def rehash {
    let old_buckets = @buckets

    @capacity = @capacity * 2
    @resize_threshold = (LOAD_FACTOR * @capacity.to_float).to_integer
    @buckets = Array.new
    @buckets[@capacity - 1] = Nil

    old_buckets.each_with_index do (pair, index) {
      pair.if_true {
        pair.reset_distance
        _rehash_pair(pair!)
      }
    }
  }

  ## Hashes the supplied key using the internal hasher of this `Map`.
  def _hash_key(key: K) -> Integer {
    let hasher = @random_state.to_hasher

    key.hash(hasher)

    hasher.to_hash
  }

  ## Inserts a new pair into `self`.
  ##
  ## The `Pair` to insert must be pre-hashed using the `Hasher` used internally
  ## by this `Map`, otherwise it might not be retrieved later.
  def _insert_pair(mut pair: Pair!(K, V)) {
    let mut index = _desired_bucket(pair.hash)

    {
      let existing = @buckets[index]

      existing.if true: {
        existing.key == pair.key
          .if_true {
            @buckets[index] = pair

            return
          }

        existing.replace_with?(pair).if_true {
          @buckets[index] = pair
          pair = existing!
        }
      }, false: {
        @length += 1
        @buckets[index] = pair

        return
      }

      index = _desired_bucket(index + 1)
      pair.increase_distance
    }.loop
  }

  ## Rehashes an existing pair into the list of buckets.
  def _rehash_pair(mut pair: Pair!(K, V)) {
    let mut index = _desired_bucket(pair.hash)

    {
      let existing = @buckets[index]

      existing.if_false {
        @buckets[index] = pair

        return
      }

      existing.replace_with?(pair).if_true {
        @buckets[index] = pair
        pair = existing!
      }

      index = _desired_bucket(index + 1)
      pair.increase_distance
    }.loop
  }

  ## Returns the desired bucket index for the given hash.
  def _desired_bucket(hash: Integer) -> Integer {
    hash % @capacity
  }

  ## Returns the index of the bucket the key resides in.
  def _bucket_index(key: K) -> ?Integer {
    let hash = _hash_key(key)
    let mut index = _desired_bucket(hash)
    let desired = index
    let mut pair = @buckets[index]

    { pair.key != key }.while_true {
      # Finding an empty bucket can mean two things:
      #
      # 1. The initial desired bucket is not used, meaning our key definitely
      #    does not exist.
      # 2. The initial desired bucket is used, and we ran into the next
      #    available bucket. This means the key also does not exist, because it
      #    would otherwise use this available bucket.
      #
      # This early return ensures we don't iterate over all buckets if we are
      # certain we won't be able to find the key.
      pair == Nil
        .if_true {
          return
        }

      index = _desired_bucket(index + 1)

      index == desired
        .if_true {
          # We cycled through all buckets but didn't find a matching pair.
          return
        }

      pair = @buckets[index]
    }

    index
  }

  ## Shifts all pairs to the left starting at the given bucket index.
  def _backwards_shift(mut index: Integer) {
    let mut pair = @buckets[index]

    { pair.and { pair!.distance.positive? } }.while_true {
      @buckets[index - 1] = pair
      @buckets[index] = Nil

      pair.reduce_distance

      index += 1
      pair = @buckets[index]
    }
  }
}

impl Equal for Map!(K, V) {
  ## Returns `True` if `self` and the given `Map` are identical to each
  ## other.
  ##
  ## # Examples
  ##
  ## Comparing two `Map` instances:
  ##
  ##     let map1 = Map.new
  ##     let map2 = Map.new
  ##
  ##     map1['name'] = 'Alice'
  ##     map2['name'] = 'Alice'
  ##
  ##     map1 == map2 # => True
  def ==(other: Self) -> Boolean where V: Equal {
    length == other.length
      .if_false {
        return False
      }

    each do (key, value) {
      other.key?(key).if_false {
        return False
      }

      value == other[key]!
        .if_false {
          return False
        }
    }

    True
  }
}

impl Index!(K, V) for Map!(K, V) {
  ## Returns the value for the given key, if any.
  ##
  ## # Examples
  ##
  ## Getting the value of a non-existing key:
  ##
  ##     let map = Map.new
  ##
  ##     map['name'] # => Nil
  ##
  ## Getting the value of an existing key:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map['name'] # => 'Alice'
  def [](key: K) -> ?V {
    let index = _bucket_index(key)

    index.if true: {
      @buckets[index!].value
    }, false: {
      Nil
    }
  }
}

impl SetIndex!(K, V) for Map!(K, V) {
  ## Inserts the key and value in this `Map`, returning the inserted value.
  ##
  ## # Examples
  ##
  ## Inserting a new key-value pair:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice' # => 'Alice'
  def []=(key: K, value: V) -> V {
    set(key: key, value: value)
    value
  }
}

impl Length for Map!(K, V) {
  ## Returns the number of key-value pairs in this map.
  ##
  ## # Examples
  ##
  ## Using an empty map:
  ##
  ##     let map = Map.new
  ##
  ##     map.length # => 0
  ##
  ## Using a map with one key-value pair:
  ##
  ##     let mut map = Map.new
  ##
  ##     map['name'] = 'Alice'
  ##
  ##     map.length # => 1
  def length -> Integer {
    @length
  }
}
