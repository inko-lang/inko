import std::byte_array::ByteArray
import std::format::DefaultFormatter
import std::test
import std::test::assert

test.group('std::byte_array::ByteArray.new') do (g) {
  g.test('Creating a new ByteArray') {
    let bytes = ByteArray.new([10])

    assert.equal(bytes.length, 1)
  }

  g.test('Creating a ByteArray with invalid bytes') {
    assert.panic { ByteArray.new([300]) }
  }
}

test.group('std::byte_array::ByteArray.clear') do (g) {
  g.test('Removing all values from a ByteArray') {
    let bytes = ByteArray.new([10, 20, 30])

    assert.equal(bytes.clear, ByteArray.new)
    assert.equal(bytes, ByteArray.new)
  }
}

test.group('std::byte_array::ByteArray.push') do (g) {
  g.test('Pushing a byte onto the end of a ByteArray') {
    let bytes = ByteArray.new

    bytes.push(10)
    bytes.push(20)

    assert.equal(bytes, ByteArray.new([10, 20]))
  }
}

test.group('std::byte_array::ByteArray.pop') do (g) {
  g.test('Removing a value from the end of a ByteArray') {
    let bytes = ByteArray.new([10, 20])

    assert.equal(bytes.pop, 20)
    assert.equal(bytes, ByteArray.new([10]))

    assert.equal(bytes.pop, 10)
    assert.equal(bytes, ByteArray.new)

    assert.equal(bytes.pop, Nil)
  }
}

test.group('std::byte_array::ByteArray.remove_at') do (g) {
  g.test('Removing the value of an existing index') {
    let bytes = ByteArray.new([10])

    assert.equal(bytes.remove_at(0), 10)
    assert.equal(bytes, ByteArray.new)
  }

  g.test('Removing the value of an out-of-bounds index') {
    let bytes = ByteArray.new([10])

    assert.equal(bytes.remove_at(5), Nil)
    assert.equal(bytes, ByteArray.new([10]))
  }

  g.test('Removing the value of a negative index') {
    let bytes = ByteArray.new([10, 20])

    assert.equal(bytes.remove_at(-2), 10)
    assert.equal(bytes, ByteArray.new([20]))
  }
}

test.group('std::byte_array::ByteArray.each') do (g) {
  g.test('Iterating over the values in a ByteArray') {
    let input = ByteArray.new([10, 20, 30])
    let output = []

    input.each do (byte) {
      output.push(byte)
    }

    assert.equal(output, [10, 20, 30])
  }
}

test.group('std::byte_array::ByteArray.each_with_index') do (g) {
  g.test('Iterating over the values and indexes in a ByteArray') {
    let input = ByteArray.new([10, 20, 30])
    let values = []
    let indexes = []

    input.each_with_index do (byte, index) {
      values.push(byte)
      indexes.push(index)
    }

    assert.equal(values, [10, 20, 30])
    assert.equal(indexes, [0, 1, 2])
  }
}

test.group('std::byte_array::ByteArray.[]') do (g) {
  g.test('Returning the value of an existing index') {
    let bytes = ByteArray.new([10, 20])

    assert.equal(bytes[0], 10)
    assert.equal(bytes[1], 20)
  }

  g.test('Returning the value of a non-existing index') {
    let bytes = ByteArray.new([10, 20])

    assert.equal(bytes[3], Nil)
  }

  g.test('Returning the value of a negative index') {
    let bytes = ByteArray.new([10, 20])

    assert.equal(bytes[-1], 20)
    assert.equal(bytes[-2], 10)
    assert.equal(bytes[-3], 20)
  }
}

test.group('std::byte_array::ByteArray.[]=') do (g) {
  g.test('Setting the value of a valid index') {
    let bytes = ByteArray.new([10, 20])

    bytes[0] = 30
    bytes[1] = 40

    assert.equal(bytes, ByteArray.new([30, 40]))
  }

  g.test('Setting the value of an invalid index') {
    assert.panic { ByteArray.new([10])[4] = 1 }
  }
}

test.group('std::byte_array::ByteArray.to_string') do (g) {
  g.test('Converting a ByteArray to a String') {
    let bytes = ByteArray.new([105, 110, 107, 111])

    assert.equal(bytes.to_string, 'inko')
  }

  g.test('Converting a ByteArray with invalid UTF-8 sequences to a String') {
    let bytes = ByteArray
      .new([72, 101, 108, 108, 111, 32, 240, 144, 128, 87, 111, 114, 108, 100])

    assert.equal(bytes.to_string, "Hello ï¿½World")
  }
}

test.group('std::byte_array::ByteArray.drain_to_string') do (g) {
  g.test('Draining a ByteArray into a String') {
    let bytes = ByteArray.new([105, 110, 107, 111])

    assert.equal(bytes.drain_to_string, 'inko')
    assert.equal(bytes, ByteArray.new)
  }
}

test.group('std::byte_array::ByteArray.to_array') do (g) {
  g.test('Converting a ByteArray to an Array') {
    let bytes = ByteArray.new([105, 110, 107, 111])

    assert.equal(bytes.to_array, [105, 110, 107, 111])
  }
}

test.group('std::byte_array::ByteArray.==') do (g) {
  g.test('Comparing two identical ByteArrays') {
    assert.equal(ByteArray.new([10]), ByteArray.new([10]))
  }

  g.test('Comparing two ByteArrays with a different length') {
    assert.not_equal(ByteArray.new, ByteArray.new([10]))
  }
}

test.group('std::byte_array::ByteArray.length') do (g) {
  g.test('Obtaining the number of bytes in a ByteArray') {
    let bytes = ByteArray.new([10, 20, 30])

    assert.equal(bytes.length, 3)
  }
}

test.group('std::byte_array::ByteArray.inspect') do (g) {
  g.test('Inspecting a ByteArray') {
    let bytes = ByteArray.new([10, 20, 30])

    assert.equal(bytes.inspect, 'ByteArray { 3 bytes }')
  }
}

test.group('std::byte_array::ByteArray.format') do (g) {
  g.test('Formatting a ByteArray') {
    let formatter = DefaultFormatter.new
    let bytes = ByteArray.new([10, 20, 30])

    bytes.format_for_inspect(formatter)

    assert.equal(formatter.to_string, 'ByteArray { 3 bytes }')
  }
}

test.group('std::byte_array::ByteArray.iter') do (g) {
  g.test('Iterating over the bytes in a ByteArray') {
    let bytes = ByteArray.new([10, 20])
    let iter = bytes.iter

    assert.equal(iter.next, 10)
    assert.equal(iter.next, 20)
    assert.equal(iter.next, Nil)
  }
}

test.group('std::byte_array::ByteArray.slice') do (g) {
  g.test('Slicing a ByteArray with a positive start position') {
    let bytes = 'hello_world'.to_byte_array

    assert.equal(bytes.slice(0, 5), 'hello'.to_byte_array)
    assert.equal(bytes.slice(0, 20), 'hello_world'.to_byte_array)
  }

  g.test('Slicing a ByteArray with a negative start position') {
    let bytes = 'hello_world'.to_byte_array

    assert.equal(bytes.slice(-5, 2), 'wo'.to_byte_array)
    assert.equal(bytes.slice(-5, 5), 'world'.to_byte_array)
    assert.equal(bytes.slice(-1, 5), 'd'.to_byte_array)
  }
}
