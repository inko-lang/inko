import std::env
import std::fs::path::(Path, SEPARATOR)
import std::os
import std::test
import std::test::assert
import std::time::SystemTime
import test::features
import test::fixtures::(INVALID_TIME, VALID_DIRECTORY, VALID_FILE)

let WINDOWS_DRIVES = Array.new(
  'a:\\', 'b:\\', 'c:\\', 'd:\\', 'e:\\', 'f:\\', 'g:\\', 'h:\\', 'i:\\',
  'j:\\', 'k:\\', 'l:\\', 'm:\\', 'n:\\', 'o:\\', 'p:\\', 'q:\\', 'r:\\',
  's:\\', 't:\\', 'u:\\', 'v:\\', 'w:\\', 'x:\\', 'y:\\', 'z:\\',
  'A:\\', 'B:\\', 'C:\\', 'D:\\', 'E:\\', 'F:\\', 'G:\\', 'H:\\', 'I:\\',
  'J:\\', 'K:\\', 'L:\\', 'M:\\', 'N:\\', 'O:\\', 'P:\\', 'Q:\\', 'R:\\',
  'S:\\', 'T:\\', 'U:\\', 'V:\\', 'W:\\', 'X:\\', 'Y:\\', 'Z:\\',

  'a:/', 'b:/', 'c:/', 'd:/', 'e:/', 'f:/', 'g:/', 'h:/', 'i:/',
  'j:/', 'k:/', 'l:/', 'm:/', 'n:/', 'o:/', 'p:/', 'q:/', 'r:/',
  's:/', 't:/', 'u:/', 'v:/', 'w:/', 'x:/', 'y:/', 'z:/',
  'A:/', 'B:/', 'C:/', 'D:/', 'E:/', 'F:/', 'G:/', 'H:/', 'I:/',
  'J:/', 'K:/', 'L:/', 'M:/', 'N:/', 'O:/', 'P:/', 'Q:/', 'R:/',
  'S:/', 'T:/', 'U:/', 'V:/', 'W:/', 'X:/', 'Y:/', 'Z:/',
)

test.group('std::fs::Path.file?') do (g) {
  g.test('Checking if a Path points to a file') {
    assert.true(VALID_FILE.file?)
    assert.false(VALID_DIRECTORY.file?)
  }
}

test.group('std::fs::Path.directory?') do (g) {
  g.test('Checking if a Path points to a directory') {
    assert.true(VALID_DIRECTORY.directory?)
    assert.false(VALID_FILE.directory?)
  }
}

test.group('std::fs::Path.exists?') do (g) {
  g.test('Checking if a file exists') {
    assert.true(VALID_FILE.exists?)
    assert.false(Path.new('does-not-exist.inko').exists?)
  }
}

test.group('std::fs::Path.to_string') do (g) {
  g.test('Converting a Path to a String') {
    assert.equal(Path.new('foo.inko').to_string, 'foo.inko')
  }
}

test.group('std::fs::Path.size') do (g) {
  g.test('Obtaining the size of an existing file') {
    assert.greater(try! VALID_FILE.size, 0)
  }

  g.test('Obtaining the size of a non existing file') {
    let path = Path.new('does-not-exist.inko')
    let size = try path.size else -1

    assert.equal(size, -1)
  }
}

test.group('std::fs::Path.==') do (g) {
  g.test('Comparing a Path with another Path') {
    assert.equal(Path.new('foo.inko'), Path.new('foo.inko'))
    assert.not_equal(Path.new('foo.inko'), Path.new('bar.inko'))
  }
}

test.group('std::fs::Path.to_path') do (g) {
  g.test('Converting a Path to another Path') {
    assert.equal(VALID_FILE.to_path, VALID_FILE)
  }
}

features.creation_time?.if_true {
  test.group('std::fs::Path.created_at') do (g) {
    g.test('Obtaining the creation time of a Path') {
      assert.not_equal(try VALID_FILE.created_at else INVALID_TIME, INVALID_TIME)
    }
  }
}

features.modification_time?.if_true {
  test.group('std::fs::Path.modified_at') do (g) {
    g.test('Obtaining the modification time of a Path') {
      assert.not_equal(try VALID_FILE.modified_at else INVALID_TIME, INVALID_TIME)
    }
  }
}

features.access_time?.if_true {
  test.group('std::fs::Path.accessed_at') do (g) {
    g.test('Obtaining the access time of a Path') {
      assert.not_equal(try VALID_FILE.accessed_at else INVALID_TIME, INVALID_TIME)
    }
  }
}

test.group('std::fs::path::Path.absolute?') do (g) {
  g.test('Checking if a Path is absolute') {
    assert.false(Path.new('foo').absolute?)
    assert.false(Path.new('..').absolute?)
    assert.true(Path.new(SEPARATOR + 'foo').absolute?)

    # On Windows /foo (or any path starting with /) is also absolute.
    assert.true(Path.new('/foo').absolute?)
  }

  os.windows?.if_true {
    g.test('Checking if a Path with a Windows drive letter is absolute') {
      WINDOWS_DRIVES.each do (drive) {
        assert.true(Path.new(drive + 'foo').absolute?)
      }
    }
  }
}

test.group('std::fs::path::Path.relative?') do (g) {
  g.test('Checking if a Path is relative') {
    assert.true(Path.new('foo').relative?)
    assert.true(Path.new('..').relative?)
    assert.false(Path.new(SEPARATOR + 'foo').relative?)
  }

  os.windows?.if_true {
    g.test('Checking if a Path with a Windows drive letter is relative') {
      WINDOWS_DRIVES.each do (drive) {
        assert.false(Path.new(drive + 'foo').relative?)
      }
    }
  }
}

test.group('std::fs::path::Path.join') do (g) {
  g.test('Joining a Path with a relative path as a String') {
    let path = Path.new('foo')
    let expected = os.windows?.if(
      true: { Path.new('foo\\bar') },
      false: { Path.new('foo/bar') }
    )

    assert.equal(path.join('bar'), expected)
  }

  g.test('Joining a Path with an absolute path as a String') {
    let path = Path.new('foo')
    let absolute = os.windows?.if(
      true: { '\\bar' },
      false: { '/bar' }
    )

    assert.equal(path.join(absolute), Path.new(absolute))
  }

  os.windows?.if_true {
    g.test('Joining a Path with an absolute path including a drive letter') {
      let path = Path.new('foo')

      WINDOWS_DRIVES.each do (drive) {
        assert.equal(path.join(drive + 'foo'), Path.new(drive + 'foo'))
      }
    }
  }

  g.test('Joining a Path with a relative path as a Path') {
    let path = Path.new('foo')
    let expected = os.windows?.if(
      true: { Path.new('foo\\bar') },
      false: { Path.new('foo/bar') }
    )

    assert.equal(path.join(Path.new('bar')), expected)
  }

  g.test('Joining a Path with an absolute path as a Path') {
    let path = Path.new('foo')
    let absolute = os.windows?.if(
      true: { Path.new('\\bar') },
      false: { Path.new('/bar') }
    )

    assert.equal(path.join(absolute), absolute)
  }

  g.test('Joining a Path with a trailing field separator') {
    let path = Path.new('foo' + SEPARATOR)
    let expected = os.windows?.if(
      true: { Path.new('foo\\bar') },
      false: { Path.new('foo/bar') }
    )

    assert.equal(path.join('bar'), expected)
  }
}

test.group('std::path::Path.directory') do (g) {
  g.test('Obtaining the directory of an absolute Path') {
    os.windows?.if_true {
      assert.equal(Path.new('C:/foo/bar').directory, Path.new('C:/foo'))
      assert.equal(Path.new('C:/foo').directory, Path.new('C:/'))
      assert.equal(Path.new('C:/').directory, Path.new('C:/'))

      assert.equal(Path.new('C:\\foo\\bar').directory, Path.new('C:\\foo'))
      assert.equal(Path.new('C:\\foo').directory, Path.new('C:\\'))
      assert.equal(Path.new('C:\\').directory, Path.new('C:\\'))
    }

    assert.equal(Path.new('/foo/bar').directory, Path.new('/foo'))
    assert.equal(Path.new('/foo/bar/..').directory, Path.new('/foo/bar'))
    assert.equal(Path.new('/foo').directory, Path.new('/'))
    assert.equal(Path.new('/').directory, Path.new('/'))
  }

  g.test('Obtaining the directory of a Path with trailing separators') {
    os.windows?.if_true {
      assert.equal(Path.new('C:\\foo\\bar\\').directory, Path.new('C:\\foo'))
      assert.equal(Path.new('C:\\foo\\').directory, Path.new('C:\\'))

      assert.equal(Path.new('C:/foo/bar/').directory, Path.new('C:/foo'))
      assert.equal(Path.new('C:/foo/').directory, Path.new('C:/'))
    }

    assert.equal(Path.new('/foo/bar/').directory, Path.new('/foo'))
    assert.equal(Path.new('/foo/').directory, Path.new('/'))
  }

  g.test('Obtaining the directory of a Path containing multiple separators') {
    os.windows?.if_true {
      assert.equal(
        Path.new('C:\\foo\\\\bar\\\\baz').directory,
        Path.new('C:\\foo\\\\bar')
      )
    }

    assert.equal(Path.new('/foo//bar//').directory, Path.new('/foo'))
    assert.equal(Path.new('/foo//bar//baz').directory, Path.new('/foo//bar'))
  }

  g.test('Obtaining the directory of a relative Path') {
    os.windows?.if_true {
      assert.equal(Path.new('foo\\bar').directory, Path.new('foo'))
    }

    assert.equal(Path.new('foo/bar').directory, Path.new('foo'))
    assert.equal(Path.new('foo').directory, Path.new('.'))
    assert.equal(Path.new('.').directory, Path.new('.'))
    assert.equal(Path.new('./foo').directory, Path.new('.'))
    assert.equal(Path.new('').directory, Path.new('.'))
  }
}
